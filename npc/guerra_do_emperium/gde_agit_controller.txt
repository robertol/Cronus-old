//===== Cronus Script ======================================
//= Guerra do Emperium - Inicio Automático da WoE
//===== Por: ==================================================
//= kalen (1.0)
//= 1.1 by Akaru and ho|yAnge|
//===== Versão Atual: =====================================
//= 2.0a
//===== Descrição: =========================================
//= Início Automático da Guerra do Emperium
//=============================================
//= gettime(3): Gets hour (24 hour time)
//= gettime(4): Gets day of week 1=Monday, 2=Tuesday,
//= 3=Wednesday, 4=Thursday, etc.
//===== Comentários Adicionais: =================================
//= v1.1a changed OnInit to OnAgitInit.[kobra_k88]
//= v1.2 added gettime checks. removed $AgitStarted var.[kobra_k88]
//= v1.3 Moved treasure spawn time here.[kobra_k88]
//= v1.3a Implemented Shadowlady's idea to allow for different
//= start/stop times on different days.[kobra_k88]
//= 1.4 Fixed treasure chests spawn! We had to unroll some loops
//= Now they appear in castles from 00:01 to 00:24. [Lupus]
//= 1.5 Fixed WOE end messages on non-WOE days, by Avaj
//= 1.5a missing tabs [KarLaeda]
//= 1.6 Corrected multiple "WoE has begun" announces [ultramage]
//= 1.7 Commented out the WoE start and end announces. [L0ne_W0lf]
//= 1.8 Castle owners displayed when WoE starts and finished. [L0ne_W0lf]
//= 1.8a Will now report unoccupied castles at start/end. [L0ne_W0lf]
//= 1.8b Whoops. Fixed a mistake :D [L0ne_W0lf]
//= 1.9 Rearranged the time-checks so they no longer use goto. [L0ne_W0lf]
//=	Removed treasure spawning function calls. (No longer needed)
//= 2.0 Added WoE Battle Log support for broadcasts. [L0ne_W0lf]
//= 2.0a Removed some brackets
//============================================================
// Para Saber Como Configurar os Horários, vá para o Arquivo doc\woe_time_explanation.txt
// Tempos Início/Fim da WoE
//======================================
-	script	Agit_Event	-1,{
	end;

OnClock2100:	//Horário do inicio da Terça(2), Quinta(4)
OnClock2300:	//Horário do Término Terça(2), Quinta(4)
OnClock1600:	//Horário do inicio do Sábado(6)
OnClock1800:	//Horário do Término do Sábado(6)

OnAgitInit:
	// starting time checks
	if((gettime(4)==2) && (gettime(3)>=21 && gettime(3)<23) ||
	   (gettime(4)==4) && (gettime(3)>=21 && gettime(3)<23) ||
	   (gettime(4)==6) && (gettime(3)>=16 && gettime(3)<18)) {
		if (!agitcheck()) {
			AgitStart;
			callsub S_DisplayOwners;
		}
		end;
	}

	// end time checks
	if ((gettime(4)==2) && (gettime(3)==23) ||
	    (gettime(4)==4) && (gettime(3)==23) ||
	    (gettime(4)==6) && (gettime(3)==18)) { 
		if (agitcheck()) {
			AgitEnd;
			callsub S_DisplayOwners;
		}
		end;
	}
	end;

S_DisplayOwners:
	setarray .@maps$[0],"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05";
	setarray .@maps$[5],"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05";
	setarray .@maps$[10],"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05";
	setarray .@maps$[15],"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05";
	for( set .@i, 0; .@i <= 19; set .@i, .@i+1 ) {
		if (GetCastleData(.@maps$[.@i],1))
			Announce "O Castelo de  [" + GetCastleName(.@maps$[.@i]) + "] Foi Conquistado Pelo Clã [" + GetGuildName(GetCastleData(.@maps$[.@i],1)) + "].",bc_all|bc_woe;
		else
			Announce "O Castelo de [" + GetCastleName(.@maps$[.@i]) + "] está Totalmente Desocupado.",bc_all|bc_woe;
	}
	end;
}
