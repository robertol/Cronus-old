//===== Cronus Script =======================================
//= Reino da Valquíria
//===== Por: ================================================
//= Nana, reparos por Poki
//= terminado, otimizado e testado por Lupus
//= L0ne_W0lf
//===== Versão Atual: =======================================
//= 3.0b
//===== Compatível Com: =====================================
//= Cronus SVN
//===== Descrição: ==========================================
//= [Conversão Aegis]
//= Missão para se tornar uma Transclasse. (Avançada/Renascida)
//===== Comentários Adicionais: =============================
//= 1.1 Made all into functions,additional checks,quest skills,now Valhallana can return you back. She also makes reborn process only when you got no Zeny/items (as in kRO) [Lupus]
//= 1.2 now alternative classes can use their 1-1 job NPC to become Advanced 1st Class [Lupus]
//= 1.4 added Baby Class support [Lupus]
//= 1.5 Fixed loads of gramatical errors. [Mass Zero]
//= 1.6 Added a func which prevent advanced classes passing 2nd Job Quests again. It also guides adv classes where to go. [Lupus]
//= 1.7 On reborn reset some Job Quest Vars and Reset Girls Counter [Lupus]
//= 1.8 Added Taekwondo classes check [Lupus]
//= 1.9 Added support for Job NPC Fase pics, sorrected one dialogue stuff [Lupus]
//= 2.0 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 2.1 Changed all numbers into constants. [Vicious]
//= 2.2 Now ALL quests skills are saved/restored [Lupus]
//= 2.3 Removed Duplicates [Silent]
//= 2.4 Fixed typos, optimized [Lupus]
//= 2.5 Fixed a stuck bug [Lupus]
//= 2.6a Adding proper dialogues [Lupus]
//= 2.7 Rescripted to Aegis 10.3 standards. [L0ne_W0;f]
//=	Removed all functions as they are no longer required.
//=	Correct job level requirements for 1-1 -> 2-x.
//= 2.7a Added Function "F_BlockHigh" back in until rewriteing of the 2-x [L0ne_W0lf]
//=	scripts are comepleted.
//= 2.7b Removed function "F_BlockHigh" as it's no longer used. [L0ne_W0lf]
//= 2.8 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.9 Corrected Valkyrie case spicifying where to warp after rebirthing. [L0ne_W0lf]
//= 2.9b Fixed Verdandi typo, fixed resetting vars [Lupus]
//= 2.9c Traduzido. [Heaven]
//= 2.9d Revisado. [.zuport]
//= 3.0 Adicionados comandos do Quest Log. [LEOzinn]
//= 3.0a Removido show, additem	[Yum]
//= 3.0b Readicinado F_BlockHigh temporariamente [Mara]
//===========================================================

valkyrie,48,86,4	script	Valquíria#	811,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Valquíria]";
		mes "Bem-vindo";
		mes "a Valhalla,";
		mes "o Salão da Honra.";
		next;
		mes "[Valquíria]";
		mes "Por favor, sinta-se";
		mes "confortável enquanto";
		mes "estiver aqui.";
		mes "Honra aos guerreiros.";
		close;
	}
	else if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[Valquíria]";
		mes "Bem-vindo";
		mes "a Valhalla,";
		mes "o Salão da Honra.";
		next;
		mes "[Valquíria]";
		mes "Por favor, sinta-se";
		mes "confortável enquanto";
		mes "estiver aqui.";
		mes "Honra aos guerreiros.";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() != 0 || checkfalcon() != 0 || checkriding() != 0) {
			mes "[Valquíria]";
			mes "Há algumas coisas que você deve fazer";
			mes "antes de iniciarmos. Você deve";
			mes "primeiramente, esvaziar sua mente e seu corpo.";
			mes "A honra virá, quando você abandonar";
			mes "todos seus desejos egoístas...";
			next;
			mes "[Valquíria]";
			mes "Você não pode levar nada com você para a próxima vida. Todos seus itens, zenys, bichinhos de estimação e carrinho têm de ser deixados para trás.";
			next;
			mes "[Valquíria]";
			mes "Quando você estiver pronto,";
			mes "por favor volte aqui,";
			mes "bravo aventureiro.";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[Valquíria]";
			mes "Vejo que você";
			mes "já se libertou de";
			mes "todas as ''ligações com o mundo'',";
			mes strcharinfo(0)+".";
			next;
			mes "[Valquíria]";
			mes "Essa é uma atitude admirável para um aventureiro como você. A honra vem quando você abandona todos seus desejos pessoais para o bem da humanidade.";
			next;
			if (SkillPoint) {
				mes "[Valquíria]";
				mes "Hmm... Sinto que você tem";
				mes "algum vínculo prolongado ou";
				mes "um trabalho inacabado em";
				mes "sua vida atual. Cuide disso,";
				mes "para encerrar sua vida presente.";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[Valquíria]";
			mes "Agora, deixe-me remover";
			mes "todas as suas memórias presentes...";
			mes "No entando, você será capaz de";
			mes "lembrar-se dos momentos mais";
			mes "honrosos de sua vida.";
			next;
			mes "[Valquíria]";
			mes "Primeiro,";
			mes "vou perdir à";
			mes "deusa Urd para remover";
			mes "todas as suas memórias";
			mes "presentes.";
			next;
			mes "[Valquíria]";
			mes "Segundo,";
			mes "vou perdir ao";
			mes "deus Verdandi para manter";
			mes "as recordações dos momentos mais honrosos de sua vida presente.";
			next;
			mes "[Valquíria]";
			mes "Terceiro,";
			mes "vou pedir ao";
			mes "deus Skuld para";
			mes "guiar você para a";
			mes "próxima vida.";
			next;
			mes "[Valquíria]";
			mes "Um...";
			callfunc "F_ClearJobVar";
			next;
			mes "[Valquíria]";
			mes "Um...";
			mes "Dois......";
			next;
			mes "[Valquíria]";
			mes "Um...";
			mes "Dois......";
			mes "e Três.";
			set ADVJOB, Class+Job_Novice_High; //memo the target 3rd Job ID
			if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			set MISC_QUEST,MISC_QUEST | 1024; //<-reset Skill Reset Event
			skill 142,1,0;
			skill 143,1,0;
			completequest 1000;
			next;
			mes "[Valquíria]";
			mes "Parabéns.";
			mes "Você agora renasceu";
			mes "em uma nova vida.";
			mes "Por favor, tome estes pequenos presentes";
			mes "em preparação para suas novas aventuras.";
			getitem 1202,1; // Faca_
			getitem 2302,1; // Camisa de Algodão_
			next;
			mes "[Valquíria]";
			mes "Desejo que a libertação da deusa Urd lhe concedeu prove ser uma Benção. Espero que as memórias que Verdandi deixou gravadas sempre te honrarão.";
			next;
			mes "[Valquíria]";
			mes "E peço que em sua nova vida a deusa Skuld oriente-o, e será mais honroso que em sua última vida.";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				warp "izlude",94,103; break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[Valquíria]";
		mes "Bem-vindo";
		mes "a Valhalla,";
		mes "o Salão da Honra.";
		next;
		mes "[Valquíria]";
		mes "Infelizmente, você não foi convidado aqui. Peço que se retire imediatamente. Honra aos guerreiros!";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	Bibliotecário#Library	742,{
	if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 0) {
			mes "[Bibliotecário]";
			mes "Bem-vindo à Biblioteca Acadêmica de Magia em Schweicherbil.";
			mes "Suponho que veio aqui";
			mes "para ler o 'Livro de Ymir.'";
			next;
			mes "[Bibliotecário]";
			mes "Infelizmente, a cópia original do livro foi danificada ao longo do tempo. Nós geralmente, autorizamos o público para ver a cópia do livro.";
			next;
			mes "[Bibliotecário]";
			mes "Além disso, a fim de preservar o original 'Livro de Ymir,' nós decidimos aceitar doações de pessoas que desejam ler a cópia que temos fornecido.";
			next;
			mes "[Bibliotecário]";
			mes "A doação";
			mes "sugerida é:";
			mes "1,285,000 zeny.";
			next;
			if (select("Doar.:Cancelar.") == 1) {
				if (Zeny >= 1285000) {
					set zeny,zeny-1285000;
					set valkyrie_Q,1;
					setquest 1000;
					mes "[Bibliotecário]";
					mes "Obrigado, sua doação será utilizada para uma boa causa. Agora você";
					mes "pode ir e ler o livro.";
					close;
				}
				mes "[Bibliotecário]";
				mes "Infelizmente, você parece não possuir zeny suficiente nesse momento. Por favor, verifique seus fundos e volte novamente.";
				close;
			}
			mes "[Bibliotecário]";
			mes "Não tenha pressa, e";
			mes "aprecie suas viagens.";
			close;
		}
		mes "[Bibliotecário]";
		mes "Mais uma vez, obrigado pela sua generosa doação. Sinta-se livre para ler a cópia em papel carbono do 'Livro de Ymir' para o seu lazer.";
		close;
	}
	mes "[Bibliotecário]";
	mes "Bem-vindo à Biblioteca Acadêmica de Magia em Schweicherbil.";
	mes "Aqui, nós temos inúmeros livros. Por favor, tome seu tempo e olhe à sua volta";
	close;
}

yuno_in02,93,207,1	script	Livro de Ymir	111,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Livro de Ymir]";
		mes "...A entrada ao Salão da Honra";
		mes "está aberta a todos que irão se mudar na sua próxima vida.";
		mes "Isso está lá para ajudar os heróis à decidir-se o que querem fazer,";
		mes "e pode levá-los para qualquer lugar nesse mundo.";
		next;
		mes "[Livro de Ymir]";
		mes "No Salão da Honra, tudo é preparado perfeitamente para heróis.";
		mes "Espalha-se boatos que todo o desejo que";
		mes "não puder ser cumprido em nossa realidade";
		mes "pode ser realizado no Salão da Honra.";
		next;
		if(select("Parar de ler.:Continuar lendo.") == 1) {
			mes "[Livro de Ymir]";
			mes ".....";
			close;
		}
		mes "[Livro de Ymir]";
		mes "Há um trajeto esquecido que leva";
		mes "ao Salão da Honra, o lugar o mais próximo aos céus. As pessoas comuns";
		mes "nunca descobrirão esse lugar...";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[Livro de Ymir]";
			mes "...Portanto, os heróis antigos estavam";
			mes "sempre na angústia, sabendo que";
			mes "eventualmente, eram mortais e";
			mes "passariam deste reino...";
			next;
			mes "[Livro de Ymir]";
			mes "Não havia nenhum documento, canção";
			mes "ou folclore restante que";
			mes "teve qualquer informação da vida";
			mes "após a morte. Entretanto, eu recentemente";
			mes "descobri um pergaminho velho sobre";
			mes "Valquíria...";
			next;
			mes "[Livro de Ymir]";
			mes "Valquíria...";
			mes "O lendário anjo guardião.";
			mes "Anjo do Ragnarok.";
			next;
			mes "[Livro de Ymir]";
			mes "Aventureiros de grande força";
			mes "e braveza serão liderados por";
			mes "Valquíria até Valhalla, o Salão";
			mes "da Honra. Lá, serão";
			mes "entregados à uma nova vida.";
			next;
			mes "[Livro de Ymir]";
			mes "Renascido, viverão novamente";
			mes "como grande heróis que";
			mes "iluminam o mundo. Os corpos";
			mes "que foram desgastados se encherão";
			mes "de energia...";
			next;
			mes "[Livro de Ymir]";
			mes "E às suas almas serão dadas";
			mes "habilidades com o coração de Ymir.";
			mes "Entretando, o coração de Ymir foi";
			mes "totalmente destruído e despresado";
			mes "pelo mundo inteiro, após a batalha de Rune-Midgard.";
			next;
			mes "[Livro de Ymir]";
			mes "Eu havia encontrado alguns pedaços";
			mes "do coração de Ymir a muito...";
			mes "...muito tempo atrás. Mas eu não posso";
			mes "confirmar se a história de Valquíria";
			mes "e Valhalla é verdadeira ou não";
			mes "através de testes científicos.";
			next;
			mes "[Livro de Ymir]";
			mes "Assim, eu estou deixando esse registro";
			mes "e espero que alguém no futuro";
			mes "possa confirmar se Valquíria e Valhalla";
			mes "existiam através de meus registros.";
			next;
			set valkyrie_Q,2;
			mes "[Livro de Ymir]";
			mes "Deixe as vidas, novas vidas dos heróis";
			mes "assim para protegerem a terra do perigo.";
			mes "E então......";
			mes "...........";
			close;
		}
		mes "[Livro de Ymir]";
		mes "...";
		close;
	}
	mes "[Livro de Ymir]";
	mes "...";
	close;
}

yuno_in05,49,43,1	script	Coração de Ymir	111,{
	if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	Teleportador	124,{
	if (Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Teleportador]";
			mes "Parabéns.";
			mes "Honra aos guerreiros!";
			close;
		}
		mes "[Teleportador]";
		mes "Por favor, evite";
		mes "de tocar em qualquer";
		mes "uma das exposições.";
		mes "..........";
		close;
	}
	else {
		mes "[Teleportador]";
		mes "Honorável,";
		mes "qual lugar você deseja ir?";
		next;
		switch(select("Prontera:Morroc:Payon:Geffen:Alberta:Izlude:Al De Baran:Comodo:Juno")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6: callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
S_WarpChar:
	mes "[Teleportador]";
	mes "Tenha uma boa viagem.";
	close2;
	savepoint getarg(0),getarg(1),getarg(2);
	warp getarg(0),getarg(1),getarg(2);
	end;
}

// Warps
//============================================================ 
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75

// function GUIDE player to Valhalla for getting 3rd JOB 
// this function prevents passing 2nd JOB QUESTS by advanced classes also 
//getarg(0) - High Job ID 
//getarg(1) - High Job Name 
//getarg(2) - 3rd Job ID 
//getarg(3) - 3rd Job Name 
//getarg(4) - NPC Name 
function	script	F_BlockHigh	{
	if(Upper != 1) return; //This func should interact with Advanced classes only 
	mes "["+getarg(4)+"]"; 
	if(Class == getarg(0) && ADVJOB == getarg(2)) goto L_RIGHTHIGH; 
	if(Class == getarg(2)) goto L_RIGHT3RD; 
	if(Class >= Job_Lord_Knight && Class <= Job_Paladin2) goto L_ALREADY3RD; //already advanced class, but from wrong guild 
	//this player is a High Novice 
	if(Class == Job_Novice_High) mes "Hello, Novice High! If you are going to became a "+getarg(1)+", then go visit your very first job teacher."; 
	//this player has 1st advanced job, but from wrong guild 
	if(Class == getarg(0)) mes "A "+getarg(1)+"?"; 
	if(Class != Job_Novice_High) mes "Rumors say only Valhallana knows your way..."; 
	emotion e_sry; 
	close; 

	L_RIGHT3RD: 
		mes "Well, hello there! You look... younger."; 
		emotion e_hmm; 
		next; 
		mes "["+getarg(4)+"]"; 
		mes "You are always welcome here, "+strcharinfo(0)+"!"; 
		mes "Our good old guild is your second home, isn't it?"; 
		emotion e_no1; 
		close;

	L_RIGHTHIGH: 
		mes "Hello, "+strcharinfo(0)+"!"; 
		mes "If you are going to become a "+getarg(3)+", then you should visit Valhalla again."; 
		if(JobLevel < 45) mes "But you need at least Job Level 45 or higher."; 
		emotion e_gasp; 
		close;
		
	L_ALREADY3RD: 
		mes "A "+getarg(3)+"?"; 
		mes "You don't belong to our guild. Begone!"; 
		emotion e_omg; 
		close; 
}