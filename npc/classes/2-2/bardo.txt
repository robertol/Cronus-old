//===== Cronus Script ======================================== 
//= Script Bardo
//===== Por: =================================================
//= Muad_Dib(The Prometheus Project), Lupus
//===== Versão Atual: ======================================== 
//= 2.1
//===== Descrição: =========================================== 
//= Muda a classe para Bardo
//===== Changelog: =========================================== 
//= Traduzido [Matheusc]
//= Quest Atualizada [Mehah]
//= Adaptado Classname Na Função F_BlockHigh [Mehah]
//= Modificado as posições de requerimento dos items [Mehah]
//= 2.0a Removido show, additem	[Yum]
//= 2.1 Script corrigido e reestruturado [Rafael]
//============================================================

comodo,226,123,5	script	Bardo Nômade	741,{
	if (Upper == 1)
	{
		mes "[Lalo]";
		mes "Escolhidos destinados a virar Deus";
		mes "são tantos nesta era";
		mes "mas eles nunca realizam seu destino enquanto vivos.";
		mes "Eles acabam se tornando homens comuns...";
		next;
		mes "[Lalo]";
		mes "Vento e nuvens, por favor envie esta mensagem a eles,";
		mes "que buscam comida, vestimenta, abrigo e riqueza.";
		mes "Conte-lhes que estão perdendo seu tempo...";
		mes "Conte-lhes que esqueceram o objetivo mais importante desta vida...";
		close2;
		cutin "job_bard_aiolo01",255;
		end;
	}
	if (BARD_Q == 1)
	{
		mes "[Lalo]";
		mes "Bem vindo! Amigo Arqueiro.";
		mes "Você trouxe a Flor? Deixe-me ver.";
		next;
		if (countitem(1032) >= 1)
		{
			mes "[Lalo]";
			mes "O que é isso?! Por favor, retire-se e coloque-se longe do alcance da minha vista.";
			mes "Eu realmente odeio flores comuns!!!";
			mes "ELAS SÃO TÃO FEIAS!";
			emotion e_omg;
			close;
		}
		setarray .@flowers, 703, 708, 629, 710, 748, 709;
		setarray .@flowers$, "aquele Hinalle", "aquela Menta", "aquela Flor Cantante", "aquela Flor da Ilusão", "aquela Rosa Eterna", "aquele Izidor";
		for (set .@i, 0; .@flowers[.@i]; set .@i, .@i + 1)
		{
			if (!countitem (.@flowers[.@i]))
				continue;
			mes "[Lalo]";
			mes "Eu posso ver " + .@flowers$[.@i] + "?";
			next;
			if (select("Sim", "Não") == 1)
			{
				if (!countitem(.@flowers[.@i]))
					break;
				delitem .@flowers[.@i], 1;
				mes "[Lalo]";
				mes "Ela não tem cheiro, mas ela é uma";
				mes "flor com moderada e graciosa.";
				mes "As folhas podem me trazer força quando eu";
				mes "usá-las para cair.";
				mes "Eu realmente gosto dessa Flor, obrigado.";
				next;
				mes "[Lalo]";
				mes "Como eu prometi, eu ajudarei você a se tornar um Bardo.";
				mes "Mas isso não é nada fácil meu amigo. Haha!";
				next;
				mes "[Lalo]";
				mes "Isto é importante que você saiba";
				mes "muitas pessoas aprendem como cantar.";
				mes "Você deverá além disso manter-se no mesmo nível com todas";
				mes "as coisas existentes nos diferentes povoados...";
				next;
				mes "[Lalo]";
				mes "Há um boneco de neve falante na cidade chamada Lutie.";
				mes "Vá para lá e traga de volta um presente.";
				next;
				mes "[Lalo]";
				mes "Se você se tornar amigo de Jack";
				mes "Frost, você receberá alguma coisa.";
				mes "E também fale com as pessoas da cidade";
				mes "enquanto você estiver lá...";
				set BARD_Q,2;
				close;
			}
		}
		if (countitem(712))
		{
			mes "[Lalo]";
			mes "Eh? Esta é apenas uma Flor normal.";
			mes "Eu gostei dela... mas ela não é o bastante.";
			mes "Por favor me traga uma Flor diferente.";
			close;
		}
		mes "[Lalo]";
		mes "Bem se adapte sozinho.";
		mes "Eu não gosto de flor em lugar algum.";
		close;
	}
	if (BARD_Q == 2)
	{
		//this var is set in LUTIE.TXT
		if (xmas_npc != 11)
		{
			mes "[Lalo]";
			mes "Eu estou esperando pelas boas novas.";
			mes "Agora vá para Lutie e faça";
			mes "amizade com Jack Frost.";
			close;
		}
		mes "[Lalo]";
		mes "Como é a viagem? Você pode encontrar muitas pessoas?";
		mes "Você deveria ter mais importancia que um presente.";
		set BARD_Q,3;
		next;
	}
	if (BARD_Q == 3)
	{
		mes "[Lalo]";
		mes "Então, você quer tentar cantar...?";
		mes "Eu cantarei uma melodia pequena...";
		mes "e vc tenta depois.";
		next;
		mes "[Lalo]";
		mes "Aqui vou eu.";
		mes "Ehem *limpando a garganta*";
		mes "1, 2, 3, 4";
		next;
		mes "[Lalo]";
		//this should text input
		mes "Todos os Deuses nunca envelhecem.";
		mes "A eternidade é uma linda Deusa";
		mes "paraíso,";
		mes "Maravilhosa e graciosa Deusa";
		mes "Paraíso,";
		mes "A nora de Odin e Bragis";
		mes "esposa";
		mes "As maçãs doces na sua cesta,";
		mes "Obrigado por suas maçãs doces.";
		next;
		mes "[Lalo]";
		mes "Agora você tenta...";
		next;
		if (select("Todos os Deuses nunca envelhecem.", "Todos os Deuses nunca envelhecem?", "Todos os Deuses nunca envelhecem,", "Os Deuses nunca envelhecem?") == 1)
			set .@score, .@score + 10;
		if (select ("A eternidade é uma linda Deusa,", "A eternidade é uma linda Deusa.", "O nunca é uma linda Deusa.", "A eternidade é uma linda Deusa") == 4)
			set .@score, .@score + 10;
		if (select ("Odins,", "Odin.", "Odin,", "paraíso,") == 4)
			set .@score, .@score + 10;
		if (select ("Deusa Muito Bonita", "Maravilhosa e Graciosa Deusa", "Maravilhosa e graciosa Deusa", "Maravilhosa e graciosa Deusa") == 3)
			set .@score, .@score + 10;
		if (select ("Paraíso.", "Odins,", "Paraíso,", "Paraísos,") == 3)
			set .@score, .@score + 10;
		if (select ("A nora de Bragis e Odin", "Bragis e Odin e sua nora", "A nora de Odin e Bragis", "A nora de Odin e Bragis") == 3)
			set .@score, .@score + 10;
		if (select ("baleia", "esposas", "esposa", "baleias") == 3)
			set .@score, .@score + 10;
		if (select ("A maçã doce na sua cesta,", "A maçã doce na sua cesta", "As maçãs doces na sua cesta,") == 3)
			set .@score, .@score + 10;
		if (select ("Obrigado por sua maçã doce!", "Obrigado por suas maçãs doces.", "Obrigado por suas maçãs doces!") == 2)
			set .@score, .@score + 10;
		next;
		if (.@score < 90)
		{
			mes "[Lalo]";
			mes "Oy, você pegou a letra da música de uma maneira errada!";
			mes "Você não pode cantar adiante...?";
			next;
			mes "[Lalo]";
			mes "Sua pronunciação é muito suja.";
			mes "Faça uma apresentação melhor da próxima vez.";
			close;
		}
		set BARD_Q,4;
		mes "[Lalo]";
		mes "Maravilhoso! Finalmente uma tentativa!";
		mes "Você pode se tornar um grande Bardo.";
		next;
		mes "[Lalo]";
		mes "Você quer mudar de classe agora?";
		mes "Ou você vai querer um presente?";
		next;
		if (select ("Apenas mude minha classe.", "Eu ficarei agradecido com um presente.") == 2)
		{
			mes "[Lalo]";
			mes "Hmmm...muito bem, traga-me alguns troncos.";
			mes "Não precisa se preocupar com o tipo, enquanto";
			mes "eles forem ^FF000060 do mesmo tipo^000000...";
			next;
			mes "[Lalo]";
			mes "Eu lhe darei um presente";
			mes "traga eles e tenha uma boa viagem.";
			close;
		}
		set .@gift, 127;
	}
	if (BARD_Q == 4)
	{
		// <requirement>,<gift>,<min. job level>
		setarray .@gifts[1], 1066,1910,50, 1066,1905,40, 1067,1903,40, 1068,1903,40, 1019,1901,40;

		if (!.@gift)
		{
			for (set .@gift, 1; .@gifts[.@gift]; set .@gift, .@gift + 3)
				if (countitem(.@gifts[.@gift]) >= 60 && JBLVL >= .@gifts[.@gift+2])
					break;

			if (!.@gifts[.@gift])
			{
				mes "[Lalo]";
				mes "Mmm? Parece que você não preparou todos os troncos ainda?";
				mes "Você quer mudar de classe assim mesmo?";	
				next;
				if (select ("Sim", "Espere, ainda não") == 2)
				{
					mes "[Lalo]";
					mes "Certo esperarei você aqui.";
					close;
				}
			}
		}
		if (skillpoint != 0)
		{
			mes "[Lalo]";
			mes "Hmmm... você parece ter algum ponto de habilidade sobrando.";
			mes "Use todos eles e depois volte aqui novamente.";
			close;
		}
		callfunc "Job_Change",Job_Bard;
		callfunc "F_ClearJobVar";
		set BARD_Q, 0;

		mes "[Lalo]";
		if (.@gifts[.@gift])
		{
			mes "Bom trabalho. Eu farei em você";
			mes "uma mudança de classe com este brinde.";
			mes "Espere apenas um momento.";
			next;
			mes "[Lalo]";
			mes "Lixa Lixa Martela Martela";
			mes "Range Range Arranha Arranha";
			next;
			if (countitem(.@gifts[.@gift]) >= 60)
			{
				delitem .@gifts[.@gift], 60;
				getitem .@gifts[.@gift+1], 1;
			}
			emotion e_grat;
			mes "[Lalo]";
			mes "Aqui está, um brinde. Isto é";
			mes "para você usar quando cantar.";
		}
		mes "Espero que cante canções felizes.";
		next;
	}
	if (BaseJob == Job_Bard)
	{
		mes "[Lalo]";
		mes "Vejo você mais tarde!";
		close;
	}
	if (BaseJob == Job_Archer && Sex == 1)
	{
		mes "[Lalo]";
		mes "Olá! Caro Arqueiro.";
		mes "De que maneira uma pessoa errante como eu pode ajudar você?";
		next;
		switch (select("Você tem uma bela voz.", "Você poderia cantar pra mim, por favor?", "Não, obrigado."))
		{
		case 1:
			mes "[Lalo]";
			mes "Hoho, sua voz é especialmente bela também?";
			mes "Sempre pensou a respeito de cantar?";
			next;
			if (select("Claro que sim!", "Eu não poderia muito possivelmente...") == 2)
			{
				mes "[Lalo]";
				mes "Haha, que timidez.";
				mes "Não acredite tão pouco em você mesmo.";
				next;
				mes "[Lalo]";
				mes "Você tem talento em abundância.";
				mes "Volte novamente se você mudar seus pensamentos.";
				close;
			}
			mes "[Lalo]";
			mes "Haha, bela atitude. Você tem que ser";
			mes "Você tem que ser assim para se tornar um Bardo.";
			mes "Eu ajudarei você a se tornar um Bardo então.";
			next;
			if(JobLevel >= 40)
			{
				set JBLVL, JobLevel; // used to determine what item to get at the end
				mes "[Lalo]";
				mes "Mas antes disso... você acredita que possa trazer para mim uma Flor?";
				mes "Eu preciso cheirar a fragância de uma";
				mes "Flor para se sentir ensinando.";
				next;
				mes "[Lalo]"; 
				mes "Realmente, não importa qual é a flor, mas tente me trazer uma que eu goste.";
				mes "E não compre apenas qualquer Flor, certo?";
				set BARD_Q,1;
				close;
			}
			mes "[Lalo]";
			mes "Mas antes disso... você precisa ter";
			mes "ao menos classe 40 para ser um Bardo.";
			next;
			mes "[Lalo]";
			mes "Volte logo, certo?";
			close;

		case 2:
			mes "[Lalo]";
			mes "Uma canção... vamos ver.";
			mes "Certo, eu recebi uma...";
			next;
			mes "[Lalo]";
			mes "Eu cantarei... Tamboril da Guerra.";
			mes "*ehem... *cof... gasp... mememememe...";
			mes "1, 2, 3, 4...";
			next;
			mes "[Lalo]";
			mes "O som dos cavalos galopando sobre o horizonte";
			mes "A areia que cobre o distante sol.";
			mes "Onde milhares de olhos abrem no céu da noite";
			mes "Os castelos de fogo queimarão com força.";
			next;
			mes "[Lalo]";
			mes "Eu posso ouvir... as batidas de meu coração.";
			mes "Eu posso sentir... o sangue correndo dentro das minhas veias.";
			mes "... e o peso da minha armadura.";
			mes "Eu posso ver... meus inimigos.";
			next;
			mes "[Lalo]";
			mes "Altissímo, Altissímo, Altissímo...";
			mes "Dê força para os guerreiros!";
			mes "Grandioso, Grandioso, Grandioso...";
			mes "Que este dia nunca volte novamente!";
			next;
			mes "[Lalo]";
			mes "Trema o céu e ruja tanto através da terra.";
			mes "Faça meu coração bater novamente!";
			mes "Deixe o som proclamar, e faça as muralhas tremerem.";
			mes "Este momento nunca mais voltará!";
			next;
			mes "[Lalo]";
			mes "Hmm.. para sempre terei uma boa canção para cantar.";
			mes "Qual sua opinião? Você não acredita que é uma bela canção?";
			next;
			if (select("Sim, ela é realmente bela", "Não, realmente...") == 1)
			{
				mes "[Lalo]";
				mes "Obrigado! Se você se divertiu com minha canção, isso";
				mes "me faz feliz, também.";
				next;
				mes "[Lalo]";
				mes "Seria legal se mais pessoas";
				mes "a nossa volta viessem e cantassem...";
				mes "bem, está bom como está agora...";
				mes "hmmhmmm.";
				close;
			}
			mes "[Lalo]";
			mes "Hmmm... eu perdi meus sentidos eu ";
			mes "terei que tentar ser mais duro.";
			mes "De alguma forma... Obrigado por me escutar.";
			close;
			
		case 3:
			mes "[Lalo]";
			mes "Oh não requisitaram uma canção quando você";
			mes "correu até o Bardo não é muito dedicado.";
			mes "bem...não posso ajudar ele desde que você";
			mes "olhe como você está apressado";
			mes "de alguma forma.";
			next;
			mes "[Lalo]";
			mes "Caçar é bom... mas você não pode";
			mes "esquecer de relaxar por um tempo.";
			mes "A juventude é curta e não pode voltar";
			mes "uma vez que passou...";
			close;
		}
	}

	mes "[Lalo]";
	mes "É um belo dia para cantar, la la.";
	close;
}
