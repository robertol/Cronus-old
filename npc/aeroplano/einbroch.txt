//===== Cronus Script ========================================
//= Equipe do Aeroplano de Einbroch
//===== Por: =================================================
//= L0ne_W0lf, Muad_Dib
//===== Versão Atual: ========================================
//= 1.1
//===== Compatível Com: ======================================
//= eAthena; rAthena; Cronus; brAthena; Maya SVN.
//===== Comentários Adicionais: ==============================
//= 1.0 Versão inicial.
//= 1.1 Traduzido. [mkbu95]
//============================================================

airport,143,43,5	script	Assist. do Aeroporto#a1::air1	90,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao aeroporto. Em que posso ajudar?";
	next;
	if (select("Entrar no Aeroporto.:Cancelar.") == 1) {
		mes "[Assistente do Aeroporto]";
		mes "A taxa de entrada é de 1.200 Zenys. Se tiver um bilhete para o Aeroplano, pode entrar de graça. Gostaria de entrar?";
		next;
		if (select("Sim:Não") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Assistente do Aeroporto]";
			mes "Desculpe, mas você não tem dinheiro suficiente. A taxa de entrada é de 1.200 Zenys. Obrigado.";
			close;
		}
	}
	mes "[Assistente do Aeroporto]";
	mes "Obrigado.";
	close;
}

airport,158,43,5	duplicate(air1)	Assist. do Aeroporto#1b	90
airport,126,43,5	duplicate(air1)	Assist. do Aeroporto#1c	90

airport,143,49,3	script	Assist. do Aeroporto#2a::air2	90,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao aeroporto. Em que posso ajudar?";
	next;
	if (select("Sair do Aeroporto.:Cancelar.") == 1) {
		mes "[Assistente do Aeroporto]";
		mes "Por favor, lembre-se de que terá de pagar a taxa de entrada para entrar no aeroporto novamente. Quer sair agora?";
		next;
		if (select("Sim:Não") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Assistente do Aeroporto]";
	mes "Volte sempre. Obrigado.";
	close;
}

airport,126,51,3	duplicate(air2)	Assist. do Aeroporto#2b	90
airport,158,50,3	duplicate(air2)	Assist. do Aeroporto#2c	90

einbroch,94,267,3	script	Assist. Aeroporto#ein01	91,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao Aeroporto de Einbroch.";
	mes "Utilize esta porta para embarcar no";
	mes "Aeropano com parada em Juno, Lighthalzen e";
	mes "Hugel na República de Schwaltzvalt.";
	next;
	mes "[Assistente do Aeroporto]";
	mes "Caso Einbroch seja seu destino,";
	mes "desça as escadas e fale com o Assistente do Aeroporto";
	mes "para levá-lo ao terminal principal.";
	mes "Obrigada, e aproveite sua viagem.";
	close;
}
