//===== Cronus Script ========================================
//= Guerra do Emperium (Geffen)
//===== Por: =================================================
//= L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.3
//===== Compatível Com: ======================================
//= Cronus GIT;
//===== Descrição: ===========================================
//= NPCs referentes ao 2º Castelo de Geffen
//===== Comentários Adicionais: ==============================
//= 1.3 Adicionado diálogos Oficiais do bRO. [Axl]
//============================================================

// Guild Flag (Outside Castle)
//============================================================
-	script	Trapesac#1::OutsideFlagsG2	722,{

	set .@GID, GetCastleData("gefg_cas02",1);

	if (.@GID == 0) {
		mes " [[Decreto do Reino de Rune Midgard]]";
		mes " ";
		mes "1. Conforme o Pacto do Reino de Rune Midgard";
		mes "e o Feudo de Britoniah,";
		mes "declaro que este Castelo não possui um Senhor.";
		mes " ";
		mes "2. Ao Clã que for bravo o bastante";
		mes "e, superarando os terríveis desafios,";
		mes "quebrar o Emperium,";
		mes "será dada a posse deste Castelo";
		mes "apoiado pelo nosso Rei.";
		close;
	}
	if (getcharid(2) == .@GID) {
			mes "[ Voz Ecoante ]";
			mes "Bravo guerreiro......";
			mes "Deseja retornar ao seu lugar de glória?";
			next;
			if (select("Sim.: Não.") == 1) {
			close2;
			if (getcharid(2) == GetCastleData("gefg_cas02",1)) {
				warp "gefg_cas02",178,43;
				end;
			}
			end;
		}
		close;
	}
	mes "[Decreto do Reino de Rune Midgard]";
	mes " ";
	mes "1. Conforme o Pacto do Reino de Rune Midgard";
	mes "e o Feudo de Britoniah,";
	mes "declaro que este castelo pertence ao Clã ^ff0000" + GetGuildName(.@GID) + "^000000 .";
	mes " ";
	mes "2. O Líder do Clã ^ff0000"+ GetGuildName(.@GID) + "^000000 se chama";
	mes "^FF0000" + GetGuildMaster(.@GID) + "^000000";
	mes "Se alguém se opõe a este decreto";
	mes "desembainhe sua espada para fazer valer sua vontade.";
	close;

OnRecvCastleG02:
	FlagEmblem GetCastleData("gefg_cas02",1);
	end;
}

gef_fild13,303,243,4	duplicate(OutsideFlagsG2)	Trapesac#1-2	722
gef_fild13,312,243,4	duplicate(OutsideFlagsG2)	Trapesac#2	722
gef_fild13,290,243,4	duplicate(OutsideFlagsG2)	Trapesac#3	722
gef_fild13,324,243,4	duplicate(OutsideFlagsG2)	Trapesac#4	722

// Guild Flag (Inside Castle)
//============================================================
-	script	Trapesac#5::InsideFlagsG2	722,{
	end;

OnRecvCastleG02:
	FlagEmblem GetCastleData("gefg_cas02",1);
	end;
}

gefg_cas02,65,130,5	duplicate(InsideFlagsG2)	Trapesac#5-2	722
gefg_cas02,30,123,5	duplicate(InsideFlagsG2)	Trapesac#6	722
gefg_cas02,65,139,6	duplicate(InsideFlagsG2)	Trapesac#7	722
gefg_cas02,37,177,6	duplicate(InsideFlagsG2)	Trapesac#8	722
gefg_cas02,37,168,6	duplicate(InsideFlagsG2)	Trapesac#9	722
gefg_cas02,68,47,2	duplicate(InsideFlagsG2)	Trapesac#10	722
gefg_cas02,68,36,2	duplicate(InsideFlagsG2)	Trapesac#11	722

// Guild Flag (Town)
//============================================================
geffen,112,129,1	duplicate(InsideFlagsG2)	Trapesac#12	722

// Kafra Employee
//============================================================
gefg_cas02,23,66,3	duplicate(guildkafra)	Kafra#gefg_cas02	117

// Steward
//============================================================
gefg_cas02,12,66,5	duplicate(cm)	Esmarehk#gefg_cas02	55

// Treasure Room Exit
//============================================================
gefg_cas02,145,114,0	script	#lever_G02	111,{

	mes " ";
	mes "Há uma pequena alavanca, você deseja acioná-la?";
	next;
	if(select("Acionar.:Não acionar.") == 1) {
		close2;
		warp "gefg_cas02",12,67;
		end;
	}
	close;
}

// Guild Dungeon Switch
//============================================================
gefg_cas02,167,40,0	duplicate(gdlever)	#gefg_cas02	111

// Guild Treasure Spawns
//============================================================
gefg_cas02,140,116,0	duplicate(Gld_Trea_Spawn)	Treasure#gefg_cas02	-1

// AGIT Manager
//============================================================
gefg_cas02,176,178,0	duplicate(Gld_Agit_Manager)	Agit#gefg_cas02	-1

// Guardians
//============================================================
gefg_cas02,12,66,5	duplicate(Gld_Guard_Template)	Guardian#gefg_cas02	-1