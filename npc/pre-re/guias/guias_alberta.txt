//===== Cronus Script ========================================
//= Guida de Alberta (Oficial do bRo)
//===== Por: =================================================
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.5d
//===== Compatível com: ======================================
//= Cronus SVN
//===== Changelog: ===========================================
//= Fully working
//= 1.1 Fixed Armory coords, thanks to Freya team
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.5a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//= 1.5b Traduzido o script de acordo com o bRo [Asus]
//= 1.5c Revisado. [RoM]
//= 1.5d Removido show	[Yum]
//============================================================

alberta,23,238,4	script	Guia#alb::AlbGuide	105,{
	cutin "prt_soldier",2;
	mes "[Guia de Alberta]";
	mes "Bem-Vindo a Alberta, a cidade portuária.";
	mes "Nós estamos aqui para ajudar os visitantes da cidade. Se você tem alguma dúvida, consulte-nos.";
	mes "Se esta é a sua primeira utilizado um guia, por favor selecione 'Tutorial'.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Indicações:Limpar Indicações:Tutorial:Cancelar")) {
		case 1:
			mes "[Guia de Alberta]";
			mes "Por favor, selecione umas das opções.";
			if (.@compass_check == 0) {
				mes "Você gostaria que as indicações permaneçam no mini-mapa?";
				next;
				if (select("Sim:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Guilda dos Mercadores^000000:Loja de Armas:Loja de Utilidades:Estalagem:Cancelar")) {
				case 1:
					mes "[Guia de Alberta]";
					mes "A Guilda dos Mercadores é o lugar";
					mes "onde os Aprendizes podem tornar-se";
					mes "Mercadores. Ela se encontra ao";
					mes "sudoeste da cidade.";
					if (.@compass_check)
						viewpoint 1,33,41,2,0xFF0000;
					break;
				case 2:
					mes "[Guia de Alberta]";
					mes "A Loja de Armas se encontra no";
					mes "sul da cidade.";
					if (.@compass_check)
						viewpoint 1,117,37,3,0xFF00FF;
					break;
				case 3:
					mes "[Guia de Alberta]";
					mes "A Loja de Utilidades se encontra";				
					mes "bem no centro da cidade.";
					if (.@compass_check)
						viewpoint 1,98,154,4,0xFF00FF;
					break;
				case 4:
					mes "[Guia de Alberta]";
					mes "A Estalagem se encontra no norte";
					mes "da cidade.";
					if (.@compass_check)
						viewpoint 1,65,233,5,0xFF00FF;
					break;
				case 5:
					mes "[Guia de Alberta]";
					mes "Se você deseja apagar as indicações";
					mes "do seu mini-mapa, por favor";
					mes "selecione 'Limpar Indicações'";
					mes "no menu.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0xFF0000;
			viewpoint 2,237,41,3,0xFF00FF;
			viewpoint 2,46,345,4,0xFF00FF;
			viewpoint 2,175,220,5,0xFF00FF;
			viewpoint 2,175,220,6,0xFF00FF;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guia de Alberta]";
  			mes "Nós fomos exaustivamente treinados para servi-lo da melhor maneira possível e sem falhas.";
			mes "Assim você tem a garantia das melhores indicações possíveis.";
  			next;
  			mes "[Guia de Alberta]";
 			mes "As indicações que lhe daremos podem ser marcadas no seu mini-mapa, aperte 'Ctrl + Tab', ou clique no botão 'Mapa' no menu principal.";
			mes "Controle o seu 'zoom' no mini-mapa por meio dos símbolos '+' e '-'.";
			break;
		case 4:
			mes "[Guia de Alberta]";
 			mes "Espero que você tenha uma jornada segura e aproveite sua estadia.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "prt_soldier",255;
	end;
}

alberta,120,60,3	duplicate(AlbGuide)	Guia#2alb	105