//===== Cronus Script ========================================
//= Guias da Cidade de Izlude
//===== Por: =================================================
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.5d
//===== Compatível Com: ======================================
//= Cronus SVN
//===== Changelog: ===========================================
//= Fully working
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.5a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//= 1.5b Traduzido. [SoulBlaker]
//= 1.5c Revisado. [RoM]
//= 1.5d Removido show	[Yum]
//============================================================

izlude,121,87,6	script	Guia#iz	105,{
	cutin "prt_soldier",2;
	mes "[Guia de Izlude]";
	mes "Bem-vindo a Izlude, a";
	mes "cidade-satélite de Prontera.";
	mes "Nós estamos aqui para ajudar os";
	mes "visitantes da cidade. Se você tem";
	mes "alguma dúvida, consulte-nos.";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("Indicações:Limpar Indicações:Tutorial:Cancelar")){
		case 1:
			mes "[Guia de Izlude]";
			mes "Por favor selecione";
			mes "uma localização no";
			mes "menu a seguir.";
			if (.@compass_check == 0) {
				mes "Quer que eu marque";
				mes "as localizações";
				mes "no seu Mini-Mapa?";
				next;
				if (select("Sim:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("^FF0000Associação dos Espadachins^000000:Sede dos Espadachins:Arena:Marina de Izlude:Loja de Armas:Loja de Utilidades:Cancelar")){
				case 1:
					mes "[Guia de Izlude]";
					mes "A Associação de Espadachins";
					mes "é localizada em uma ilha";
					mes "no oeste de Izlude. Se você está pensando em se tornar um Espadachim,";
					mes "você deveria ir lá.";
					if (.@compass_check)
						viewpoint 1,52,140,0,0xFF0000;
					break;
				case 2:
					mes "[Guia de Izlude]";
					mes "A Sede dos Espadachins";
					mes "é localizada na ilha ao leste";
					mes "que está conenctada à Izlude.";
					if (.@compass_check)
						viewpoint 1,214,130,1,0x00FF00;
					break;
				case 3:
					mes "[Guia de Izlude]";
					mes "A famosa Arena de Izlude";
					mes "é localizada ao extremo";
					mes "norte de Izlude.";
					if (.@compass_check)
						viewpoint 1,128,225,2,0x00FF00;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "Você pode encontrar";
					mes "a Marina na parte";
					mes "nordeste de Izlude. Lá, você";
					mes "pode pegar um barco para";
					mes "Alberta ou à Ilha Byalan.";
					if (.@compass_check)
						viewpoint 1,200,180,3,0xFF0000;
					break;
				case 5:
					mes "[Guia de Izlude]";
					mes "Você pode facilmente";
					mes "encontrar a Loja de Armas";
					mes "ao noroeste de Izlude.";
					if (.@compass_check)
						viewpoint 1,111,149,4,0xFF00FF;
					break;
				case 6:
					mes "[Guia de Izlude]";
					mes "A Loja de Ferramentas não deve";
					mes "ser muito difícil de se encontrar";
					mes "ao nordeste de Izlude.";
					if (.@compass_check)
						viewpoint 1,148,148,5,0xFF00FF;
					break;
				case 7:
					mes "[Guia de Izlude]";
					mes "Se você deseja apagar as indicações";
					mes "do seu mini-mapa, por favor";
					mes "selecione 'Limpar indicações'.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,0,0x00FF00;
			viewpoint 2,237,41,1,0x0000FF;
			viewpoint 2,46,345,2,0x00FF00;
			viewpoint 2,175,220,3,0xFF0000;
			viewpoint 2,134,221,4,0xFF0000;
			viewpoint 2,204,214,5,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guia de Izlude]";
			mes "Nós fomos exaustivamente treinados";
			mes "para servi-lo da melhor maneira";
			mes "possível e sem falhas.";
			mes "Assim você tem a garantia das";
			mes "melhores indicações possíveis.";
			next;
			mes "[Guia de Izlude]";
			mes "As indicações que lhe daremos podem";
			mes "ser marcadas no seu mini-mapa.";
			mes "Para visualizar o seu mini-mapa,";
			mes "aperte 'Ctrl + Tab', ou clique no";
			mes "botão 'Mapa' no menu principal.";
			mes "Controle o seu 'zoom' no mini-mapa";
			mes "por meio dos símbolos '+' e '-'.";
			break;
		case 4:
			mes "[Guia de Izlude]";
			mes "Sinta-se à vontade";
			mes "para me consultar";
			mes "se você estiver perdido";
			mes "em Izlude, certo?";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}