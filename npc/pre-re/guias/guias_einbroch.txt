//===== Cronus Script ======================================== 
//= Guias de Einbroch
//===== Por: ================================================= 
//= Muad_dib; L0ne_W0lf
//===== Versão Atual: ======================================== 
//= 1.5
//===== Compatível com: ====================================== 
//= Cronus SVN
//===== Changelog: =========================================== 
//= 1.0b missing tabs, fixed Guide coords [Lupus]
//= 1.1 added 1 more Guide NPC, thanx to Justin84 [Lupus]
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.5a Traduzido. [RoM]
//============================================================

einbroch,72,202,4	script	Guia#ein::EinGuide	852,{
	cutin "ein_soldier",2;
	mes "[Guia de Einbroch]";
	mes "Bem-vindo";
	mes "a Einbroch,";
	mes "a Cidade do Aço.";
	mes "Por favor me pergunte se";
	mes "tiver alguma dúvida.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Indicações:Remover Indicações:Tutorial:Cancelar")) {
		case 1:
			mes "[Guia de Einbroch]";
			mes "Por favor selecione";
			mes "uma localização no";
			mes "menu a seguir.";
			if (.@compass_check == 0) {
				mes "Você gostaria que eu";
				mes "marcasse as localizações";
				mes "no seu Mini-Mapa?";
				next;
				if (select("Sim:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("^FF0000Aeroporto^000000:Estação de Trem:Fábrica:Praça:Estalagem:Loja de Armas:Laboratório:Guilda dos Ferreiros:Torre de Einbroch:Cancelar")) {
				case 1:
					mes "[Guia de Einbroch]";
					mes "O ^FF0000Aeroporto^000000 é localizado";
					mes "na parte noroeste da cidade.";
					mes "Lá você pode ver o nosso";
					mes "orgulho e alegria da cidade, o Aeroplano. Lembre-se que você deve pagar uma taxa para entrar no Aeroplano.";
					if (.@compass_check)
						viewpoint 1,63,228,2,0xFF0000;
					break;
				case 2:
					mes "[Guia de Einbroch]";
					mes "A Estação de Trem é";
					mes "localizada na parte";
					mes "nordeste de Einbroch. Trens";
					mes "daqui para Eeinbech saem";
					mes "a toda hora, todo dia.";
					if (.@compass_check)
						viewpoint 1,236,279,3,0xFF00FF;
					break;
				case 3:
					mes "[Guia de Einbroch]";
					mes "A Fábrica, talvez o";
					mes "mecanismo mais importante";
					mes "em Einbroch, é localizado na";
					mes "parte sul da cidade.";
					if (.@compass_check)
						viewpoint 1,158,78,4,0xFF00FF;
					break;
				case 4:
					mes "[Guia de Einbroch]";
					mes "O Plaza, nossa maior área";
					mes "de comércio, pode ser";
					mes "encontrada ao leste";
					mes "do centro de Einbroch.";
					if (.@compass_check)
						viewpoint 1,232,190,5,0xFF00FF;
					break;
				case 5:
					mes "[Guia de Einbroch]";
					mes "A Estalagem fica ao leste";
					mes "do Plaza e oferece a";
					mes "melhor acomodação.";
					mes "Lá, você pode curtir a";
					mes "sua estadia em Einbroch com conforto~";
					if (.@compass_check)
						viewpoint 1,260,201,6,0x00FF00;
					break;
				case 6:
					mes "[Guia de Einbroch]";
					mes "A Loja de Armas fica";
					mes "localizada ao norte do";
					mes "Plaza. Lá você pode";
					mes "comprar armas para";
					mes "uso pessoal.";
					if (.@compass_check)
						viewpoint 1,215,221,7,0x00FF00;
					break;
				case 7:
					mes "[Guia de Einbroch]";
					mes "O Laboratório é um";
					mes "anexo da Fábrica e";
					mes "é localizado no setor";
					mes "noroeste de Einbroch.";
					if (.@compass_check)
						viewpoint 1,36,49,8,0x00FF;
					break;
				case 8:
					mes "[Guia de Einbroch]";
					mes "A Guilda de Ferreiros é";
					mes "localizada na parte";
					mes "sudeste de Einbroch. Você";
					mes "pode refinar seus esquipamentos";
					mes "usando seus serviços.";
					if (.@compass_check)
						viewpoint 1,244,90,9,0x00FF00;
					break;
				case 9:
					mes "[Guia de Einbroch]";
					mes "A Torre de Einbroch é";
					mes "localizada no centro da";
					mes "cidade. Do topo da";
					mes "torre, você pode ver";
					mes "toda Einbroch.";
					if (.@compass_check)
						viewpoint 1,174,195,10,0xFFFF00;
					break;
				case 10:
					mes "[Guia de Einbroch]";
					mes "Por favor me peça para 'Remover";
					mes "Indicações' se você";
					mes "não quer mais ter as";
					mes "marcas de localizações mostradas";
					mes "no seu Mini-Mapa.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,63,228,2,0xFF0000;
			viewpoint 2,236,279,3,0xFF00FF;
			viewpoint 2,158,78,4,0xFF00FF;
			viewpoint 2,232,190,5,0xFF00FF;
			viewpoint 2,260,201,6,0x00FF00;
			viewpoint 2,215,221,7,0x00FF00;
			viewpoint 2,36,49,8,0x00FF00;
			viewpoint 2,244,90,9,0x00FF00;
			viewpoint 2,174,195,10,0xFFFF00;
			set .@compass_check,0;
			mes "[Guia de Einbroch]";
			mes "Tudo bem, as marcas do";
			mes "seu Mini-Mapa foram";
			mes "removidas. Se você precisar";
			mes "de alguma indicação em Einbroch,";
			mes "é só perguntar a mim ou a";
			mes "outros Guias de Einbroch.";
			break;
		case 3:
			mes "[Guia de Einbroch]";
			mes "Através da tecnologia da";
			mes "República de Schwaltzvalt,";
			mes "nós melhoramos para um sistema";
			mes "de informação digital que nos permite";
			mes "marcar as localizações no seu";
			mes "Mini-Mapa para uma navegação melhor.";
			next;
			mes "[Guia de Einbroch]";
			mes "Seu Mini-Mapa é localizado";
			mes "no canto superior direito";
			mes "da tela. Se você não puder vê-lo,";
			mes "pressione as teclas 'Ctrl + Tab'";
			mes "ou clique no botão 'Mapa' na sua Janela de Informações Básicas.";
			next;
			mes "[Guia de Einbroch]";
			mes "No seu Mini-Mapa,";
			mes "clique nos símbolos '+' e '-'";
			mes "para aumentar ou diminuir o";
			mes "zoom do seu Mini-Mapa. Nós";
			mes "esperamos que você aproveite sua";
			mes "viajem aqui em Einbroch, aventureiro.";
			break;
		case 4:
			mes "[Guia de Einbroch]";
			mes "Nós esperamos que você";
			mes "aproveite sua viagem";
			mes "aqui em Einbroch.";
			mes "Ah, por ficar fique";
			mes "atento aos Alertas de Poluição.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "ein_soldier",255;
	end;
}

einbroch,155,43,4	duplicate(EinGuide)	Guia#2ein	852

einbroch,162,317,4	duplicate(EinGuide)	Guia#3ein	852