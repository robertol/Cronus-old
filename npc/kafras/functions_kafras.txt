//===== Cronus Script ======================================
//= Kafra Functions
//===== Por: ================================================== 
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Versão Atual: ===================================== 
//= 6.8
//===== Descrição: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Comentários Adicionais: ================================= 
//= v1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//= Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=	to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=	  Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=	Need all coords for all guides.. and somehow pass their coords to kafra.
//=	Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=	Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=	Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=	Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=		Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Tradução Pt-Br [ BOO]
//============================================================ 

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	// Display Kafra Welcome Message
	switch(getarg(0)){
		default:
		case 0:
			// Default message (obsolete)
			mes "[Funcionária Kafra]";
			mes "Bem Vindo!";
			mes "A Corporação Kafra";
			mes "está sempre ao seu Lado.";
			mes "Então, como Posso Ajuda-lo?";
			break;
		case 1:
			// Niflheim specific message
			mes "[Funcionária Kafra]";
			mes "^666666B beeeemmm-Vindoo";
			mes "a-a C-coorpoor-r-raaçãooo... K-kaaafrrrra^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			set @GID,getcharid(2);
			mes "[Funcionária Kafra]";
	 		mes "Bem Vindo. ^ff0000" + GetGuildName(@GID) + "^000000 Membro.";
			mes "A Corporação Kafra estará sempre ao seu lado.";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[Funcionária Kafra]";
			mes "Então, você vem de uma terra distante para estudar nossa cultura, ou está apenas passeando?";
			mes "Em ambos os Casos, por que não fica algum tempo?";
			mes "O ar é eternamente pesado com";
			mes "o perfume de flores silvestres.";
			break;		
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[Funcionária Kafra]";
			mes "Com as Diversas Kafras";
			mes "espalhadas pelo mundo, você nunca";
			mes "estará longe de casa.";
			break;		
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	while (1) {
		next;
		deletearray @K_Menu0$[0],getarraysize(@K_Menu0$);
		if (getarg(0) == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"Use Storage","Usar Armazém","Alugar Carrinho","Usar Serviço de Teletransporte","Cancelar";
		} else {
			switch(getarg(1)){
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"Salvar","Usar Armazém","Cancelar"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"Usar Armazém","Cancelar"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Checar Outra Informação","Cancelar"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Checar Outra Informação","Cancelar"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"Usar Armazém","Checar Outra Informação","Cancelar"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Cancelar"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"Salvar","Usar Armazém","Checar Outra Informação","Cancelar"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Usar Serviço de Teletransporte","Checar Outra Informação","Cancelar"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"Salvar","Usar Armazém","Usar Serviço de Teletransporte","Alugar Carrinho","Checar Outra Informação","Cancelar"; break;
			}
		}
		set .@menu$,"";
		for (set .@i, 0; .@i < getarraysize(@K_Menu0$); set .@i, .@i + 1)
			set .@menu$, .@menu$ + @K_Menu0$[.@i] + ":";	
		set .@j, select (.@menu$) - 1;
		if (@K_Menu0$[.@j] == "Salvar") {
			return;
		} else if (@K_Menu0$[.@j] == "Usar Armazém") {
			// Do not charge for Guild Storage
			if(getarg(0) == 2) 
				callfunc "F_KafStor",2,0,0;
			else 
				callfunc "F_KafStor",0,getarg(3),getarg(0);
		} else if (@K_Menu0$[.@j] == "Usar Serviço de Teletransporte") {
			// Display Einbroch "No Teleport Service" notice.
			if(getarg(1) != 4) callfunc "F_KafTele",getarg(0);
			else {
				mes "[Funcionária Kafra]";
				mes "Por Causa do ^FF0000Acordo de";
				mes "Transporte Limitado^000000, a";
				mes "Corporação Kafra não pode";
				mes "fornecer os serviços de teletransporte";
				mes "na República Schwaltzvalt.";
				next;
				mes "[Funcionária Kafra]";
				mes "Nós Pedimos que, por favor,";
				mes "use o serviço de Dirigível.";
				mes "Obrigado por sua,";
				mes "Compreensão e Colaboração.";
			}
		} else if (@K_Menu0$[.@j] == "Alugar Carrinho") {
			if(callfunc("F_KafCart",getarg(0),getarg(4)) == 1) 
			next;
		} else if (@K_Menu0$[.@j] == "Checar Outra Informação") {
			callfunc "F_KafInfo",getarg(2);
		} else if (@K_Menu0$[.@j] == "Cancelar"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		} else if (@K_Menu0$[.@j] == "Usar Armazém") {
			callfunc "F_KafStor",1,0;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if(getarg(0) == 1){
		if(guildopenstorage()){
			mes "[Funcionária Kafra]";
			mes "Me Desculpe, mais outro Membro do Clã está usando o Armazém";
			mes "neste momento. Por Favor, aguarde até que a pessoa tenha Terminado.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
		mes "[Funcionária Kafra]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-s-ssoooorry,";
			mes "Vooo-cc-ê e-e-eeee";
			mes "u-um-mm Apprrreeenndizz... É-e";
			mes "neece-eessáriiio aaass Habbiiliidaaadess Báasicca-s N-niivel 6...^000000";
			return;
		}
		mes "Me desculpe, mas você";
		mes "precisa das";
		mes "Habilidades básicas Nivel 6 para";
		mes "usar o Armazém.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(getarg(0) != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<getarg(1)){
				mes "[Funcionária Kafra]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "M-ais z-zeny...!";
					mes "N-necessários 150... zeny...";
					mes "Ergh! Preciiso de S-angue~!^000000";
					return;
				}
				// Standard Message
				mes "Me Desculpe, mais você não";
				mes "tem Zenys Suficientes para";
				mes "o Serviço de Armazem. É";
				mes "Necessário para acessar o Armazém "+getarg(1)+" zeny.";
				return;
			}
			set Zeny, Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + (getarg(1)/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Funcionária Kafra]";
			for (set .@i, 0; .@i < 5; set .@i, .@i + 1)
				mes "^666666Obrigado.. por... Usar... Nossos Serviços...^000000";
		}
		// Normal message
		else {
			mes "[Funcionária Kafra]";
			mes "Aqui, Deixe-me Abrir";
			mes "o Armazém pra você.";
			mes "Obrigado por usar";
			mes "os Serviços Kafra.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[Funcionária Kafra]";
	mes "Por Favor , escolha";
	mes "seu Destino.";
	next;
	set .@menu$, "";
	for (set .@i, 0; .@i <= 6; set .@i, .@i + 1)
		set .@menu$, .@menu$ + @wrpC$[.@i] + ":";
	set .@j, select (.@menu$) - 1;
	if (@wrpC$[.@j] == "Cancel") return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
	else {
		if (Zeny<@wrpP[.@j]){
			mes "[Funcionária Kafra]";
			mes "Me desculpe, mais você não tem";
			mes "zeny suficientes para o serviço";
			mes "de Teleporte. Para Teleportar";
			mes "para "+@wrpD$[.@j]+" são "+@wrpP[.@j]+" zeny.";
			close2;
			cutin "", 255;
			end;
		}
		set Zeny, Zeny - @wrpP[.@j];
		set RESRVPTS, RESRVPTS + (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo,Ilha do Farol") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") { if (checkre(0)) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Monte Mjolnir") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Vila dos Orcs") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(BaseClass != Job_Merchant){
		mes "[Funcionária Kafra]";
		mes "Me desculpe, mas o";
		mes "serviço de aluguel de carrinho";
		mes "está disponível apenas para Mercador,";
		mes "Ferreiro, Mestre-Ferreiro,";
		mes "Alquimista e Bioquímico.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[Funcionária Kafra]";
		mes "Você já está com";
		mes "um Carrinho equipado.";
		mes "Infelizmente não podemos";
		mes "alugar mais de um a cada cliente";
		mes "por vez.";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Funcionária Kafra]";
		mes "O aluguel do Carrinho";
		mes "Custa "+getarg(1)+" zeny. Você";
		mes "gostaria de Aluga-lo?";
		next;
		if (select("Alugar Carrinho.:Cancelar") == 2) return 0;
			if(Zeny<getarg(1)){
				mes "[Funcionária Kafra]";
				mes "Me desculpe, mais você";
				mes "não tem zeny";
				mes "suficiente para pagar. O ";
				mes "aluguel custa "+getarg(1)+" zeny.";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//	set .@block, 1;
		setarray .@m$, "Checar Pontos Especiais Reservados.","Serviço de Senha no Armazém","Localização das Funcionárias Kafra","Cancelar";
		if (getarg(0) == 2) deletearray .@m$[1],2; // Rune Knight
		else if (.@block) deletearray .@m$[1],1;
	while (1) {
		set .@j, select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "Check Special Reserve Points.") {
			mes "[Funcionária Kafra]";
			mes "Vamor Ver...";
			mes strcharinfo(0) + "...";
			mes "Ah, você tem um total de";
			mes RESRVPTS+ " Pontos especiais reservados.";
			next;
			mes "[Funcionária Kafra]";
			mes "Você pode trocar seus pontos";
			mes "especiais por";
			mes "recompenças no escritório Kafra em Al De Baran. Por Favor, use nossos";
			mes "serviços convenientes para ver os beneficios do programa de recompenças.";
			next;
			if(getarg(0) == 1) return;
		} else if (.@m$[.@j] == "Localização das Funcionárias Kafra") {
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "Serviço de Senha no Aramzém") {
			callfunc("F_SetKafCode","[Funcionária Kafra]","Serviços Kafra");
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Funcionária Kafra]";
	if(getarg(1)==1) {	
		// Save
		mes "Seu Ponto de Retorno";
		mes "foi salvo bem aqui";
		mes getarg(2)+".";
		mes "Obrigado por Usar";
		mes "os serviços Kafra.";
	} else if(getarg(0)==0 || getarg(0)==5) {	
		// Generic End
		mes "Nós, da Corporação Kafra,";
		mes "estamos sempre nos esforçando para fornece-lhe os melhores serviços.Estamos Felizes por poder atender suas Necessidades.";
	} else if(getarg(0)==1) {
		// Niflheim End
		percentheal 0,- 25;
		mes "^666666Kaffffra n-nunca";
		mes "moooooooooooooorrre. Sempre...";
		mes "Sempre d-do seeeu laaaaaaado~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if(!#kafra_code) return;
	mes "Digite a Senha de seu Armazém:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom "Senha Incorreta.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	mes getarg(0);
	if(#kafra_code) {
		mes "Seu Armazém está Protegido com Senha. O que voce quer fazer agora?";
		next;
		switch (select("Mudar Senha do Armazém -> 5000z:Remover Senha do Armazém -> 1000z:Cancelar")) {
			Case 1:
				mes getarg(0);
				mes "mas primeiro, digite sua ^0000FFAntiga Senha^000000.";
				set @code,callfunc("F_EntKafCode");
				if(!@code || @code != #kafra_code) {
					mes "Senha Incorreta. Você não pode Alterar sua Senha.";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,getarg(0),getarg(1);
				break;
			Case 2:
				mes getarg(0);
				mes "Por Favor, Digite sua senha antes de removê-la.";
				set @code,callfunc("F_EntKafCode");
				if(!@code) {
					mes "A Senha não foi Removida.";
					emotion e_hmm;
					break;
				}
				next;
				mes getarg(0);
				if(Zeny < 1000) {
					mes "Você não tem Zeny Suficiente.";
					emotion e_cash;
					break;
				}
				set Zeny,Zeny-1000;
				if(@code == #kafra_code) {
					set #kafra_code,0;
					mes "Você Removeu a Senha do seu Armazém com Sucesso.";
					mes "Obrigado por Usar "+getarg(1)+".";
					emotion e_thx;
				} else {
					mes "Senha Incorreta. Nós Não Devolveremos 1000z.";
					mes "Por Favor, ultilize a senha correta na Próxima vez.";
					emotion e_sry;
				}
				break;
			Case 3:
				break;
		}
	} else {
		mes getarg(1)+" Orgulhosamente apresento-lhe um novo serviço:";
		mes "Proteção adicional, Armazém com Senha.";
		next;
		switch (select("Colocar uma Nova senha -> 5000z:Cancelar")) {
			Case 1: callsub S_SET,getarg(0),getarg(1); break;
			Case 2: break;		
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes getarg(0);
	mes "Agora digite a ^FF0000nova senha^000000 para proteger seu Armazém de ladrões.";
	set @code,callfunc("F_EntKafCode");
	if(!@code) {
		mes "A senha não foi Alterada.";
		emotion e_hmm;
		return;
	}
	next;
	mes getarg(0);
	if(Zeny < 5000) {
		mes "Você não tem zeny Suficiente.";
		emotion e_cash;
		return;
	}
	set Zeny,Zeny-5000;
	set #kafra_code,@code;
	mes "Você protegeu seu armazém com uma  Senha secreta.";
	mes "Obrigado por usar "+getarg(1)+".";
	emotion e_thx;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Digite um Numero entre 1000~10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: Tentaram Acessar a Senha do Armazém.";
	}
	if(input(@code_) == 1) {
		mes "Você não pode usar uma Senha tão grande.";
		return 0;
	}
	if(@code_<1000) {
		mes "Você não pode usar uma Senha tão Pequena.";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	cleararray @wrpD$[0],"",getarraysize(@wrpD$);
	cleararray @wrpP$[0],"",getarraysize(@wrpP$);
	cleararray @viewpX[0],"",getarraysize(@viewpX);
	cleararray @viewpY[0],"",getarraysize(@viewpY);
	if (strnpcinfo(4) == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Vila dos Orcs", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1200, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (strnpcinfo(4) == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (strnpcinfo(4) == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	} else if (strnpcinfo(4) == "comodo" ) {
		setarray @wrpD$[0], "Morroc", "Comodo Ilha do Farol", "Umbala";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (strnpcinfo(4) == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morroc";
		setarray @wrpP[0], 1200, 1200;
	} else if (strnpcinfo(4) == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Vila dos Orcs", "Mjolnir Dead Pit";
		setarray @wrpP[0], 1200, 1200, 1200, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (strnpcinfo(4) == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(4) == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Ilha do Farol";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (strnpcinfo(4) == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (strnpcinfo(4) == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morroc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (strnpcinfo(4) == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (strnpcinfo(4) == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}
	for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
		setd "@wrpC$["+.@i+"]",getd("@wrpD$["+.@i+"]")+" -> "+getd("@wrpP["+.@i+"]");
	setd "@wrpC$["+getarraysize(@wrpC$)+"]","Cancel";
	return;
}
