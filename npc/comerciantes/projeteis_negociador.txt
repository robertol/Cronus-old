//===== Cronus Script ===========================================
//= Bullet Dealers
//===== Por =====================================================
//= Playtester, Paradox924X
//===== Vers�o ==================================================
//= 1.7
//===== Descri��o ===============================================
//= Negociador de Proj�teis.
//===== Coment�rios =============================================
//= 1.0 First version [Playtester]
//= 1.1 Converted from Aegis [Paradox924X]
//= 1.2 More optimized conversion [Legionaire]
//= 1.2a Removed .GATs [Lupus]
//= 1.3 Fixed [Playtester] Optimized. Got rid of @vars [Lupus]
//= 1.4 Updated to match AEGIS script. [Kisuka]
//= 1.5 Updated to match AEGIS script again. [Masao]
//= 1.6 Moved Izlude duplicate to pre-re/re paths. [Euphy]
//= 1.7 Tradu��o [Shermie]
//===============================================================

alberta,118,160,3	script	Vendedor de Proj�teis#e::bdt	4_M_04,{
	if (BaseJob == Job_Gunslinger) {
		mes "[Tony]";
		mes "Eu sou Tony, o negociador de proj�teis.";
		mes "Venha a mim sempre que precisar de muni��es.";
		mes "Apenas me traga os materiais, e eu irei lhe fazer os proj�teis que precisar.";
		next;
		switch(select("Esfera Venenosa:Esfera de Chamas:Esfera de Rel�mpagos:Esfera Cegante:Esfera Congelante:Cancelar")) {
		case 1: callfunc "Bullet_Trade",937,10,13205; break;
		case 2: callfunc "Bullet_Trade",7097,2,13203; break;
		case 3: callfunc "Bullet_Trade",7053,3,13204; break;
		case 4: callfunc "Bullet_Trade",1024,5,13206; break;
		case 5: callfunc "Bullet_Trade",7054,2,13207; break;
		case 6:
			mes "[Tony]";
			mes "Hmm. Ok.";
			mes "Volte quando desejar!";
			mes "Eu, Tony, o Negociador de Proj�teis!";
			mes "Sempre estarei aqui~!!!";
			close;
		}
	}
	mes "[Tony]";
	mes "Ei, meu nome � Tony.";
	mes "Eu sou respons�vel pela distribui��o e fabrica��o";
	mes "de proj�teis para Justiceiros.";
	next;
	mes "[Tony]";
	mes "Lamento se voc� veio";
	mes "at� aqui para comprar muni��o.";
	mes "Eu s� posso negociar com Justiceiros credenciados.";
	close;
}

function	script	Bullet_Trade	{
	mes "[Tony]";
	mes "Para cada conjunto de";
	mes "30 "+getitemname(getarg(2))+"s,";
	mes "voc� tem que me dar";
	mes "1 Emveretarcon,";
	mes "1 Phracon, e";
	mes ""+getarg(1)+" "+getitemname(getarg(0))+".";
	next;
	mes "[Tony]";
	mes "Lembre que s� posso entregar um m�ximo de 500 caixas de 30 proj�teis por negocia��o.";
	mes "Por favor entre com o n�mero de caixas de proj�teis que voc� gostaria.";
	mes "Caso queira parar a negocia��o, apenas digite '0'.";
	next;
	input .@amount;
	if (.@amount < 1 || .@amount > 500) {
		mes "[Tony]";
		mes "Ei, n�o posso lhe dar tantos proj�teis.";
		mes "N�o esque�a de digitar um n�mero menor que 500 se voc� quiser trocar alguns itens por alguns proj�teis.";
		close;
	}
	if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {
			mes "[Tony]";
			mes "Nossa! Seu invent�rio n�o possui espa�o o suficiente para esta quantia de proj�teis.";
			mes "Volte quando tiver mais espa�o no invent�rio.";
			mes "Tente guardar algumas coisas no Armaz�m da Kafra.";
			close;
		} else {
			mes "[Tony]";
			mes "Muito bom, tudo parece estar em ordem.";
			mes "Deixe-me pegar esses materiais, e aqui est�o seus proj�teis.";
			mes "Foi um prazer negociar com voc�~";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			getitem getarg(2),.@amount * 30;
			close;
		}
	} else {
		mes "[Tony]";
		mes "Voc� n�o possui";
		mes "os itens que";
		mes "eu necessito.";
		mes "Prepara-os e volte.";
		close;
	}
}
