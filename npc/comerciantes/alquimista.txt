//===== Cronus Script ===========================================
//= Loja de Alquimia
//===== Por: ====================================================
//= rAthena Dev Team
//===== Vers�o Atual: ===========================================
//= 1.9
//===== Descri��o: ==============================================
//= Mercador para Materiais de Alquimistas e Manuais.
//===== Coment�rios Adicionais: =================================
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//=     Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//= 1.7 Updated to match AEGIS script. [Masao]
//= 1.8 Moved Craft Book Merchant to Renewal path. [Euphy]
//= 1.9 Tradu��o [Shermie]
//===============================================================

alde_alche,24,188,3	script	Comerciante da Guilda	2_M_ALCHE,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto! -";
		mes "- Atualmente voc� est� carregando -";
		mes "- muitos itens com voc�. -";
		mes "- Por favor, volte novamente -";
		mes "- apos deixar alguns itens no Armaz�m da Kafra. -";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Bem-vindo �";
	mes "Uni�o dos Alquimistas.";
	mes "Como posso ajud�-lo?";
	next;
	switch(select("Comprar materiais.:Comprar manual de produ��o.:Cancelar.")) {
	case 1:
		mes "[Gever Al Sharp]";
		mes "O que voc� gostaria de comprar?";
		next;
		if (select("Vasilha de Mistura - 8 Zeny:Cancelar.") == 1) {
			mes "[Gever Al Sharp]";
			mes "Quantas voc� deseja?";
			mes "Digite '0' se voc� deseja cancelar.";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[Gever Al Sharp]";
					mes "A opera��o foi cancelada.";
					mes "Volte sempre.";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[Gever Al Sharp]";
					mes "O n�mero deve";
					mes "ser inferior a 2000.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[Gever Al Sharp]";
				mes "Vejo que voc� n�o";
				mes "tem a quantidade de zeny";
				mes "suficiente. Verifique";
				mes "quanto voc� tem de zeny primeiro.";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[Gever Al Sharp]";
				mes "Me parece que voc� n�o";
				mes "pode carregar tudo.";
				mes "Verifique o espa�o";
				mes "em seu ivent�rio.";
				close;
			}
			set Zeny, Zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[Gever Al Sharp]";
			mes "Obrigado.";
			mes "Volte sempre.";
			close;
		}
		mes "[Gever Al Sharp]";
		mes "Pois bem,";
		mes "volte novamente caso";
		mes "necessite de mais";
		mes "materiais relacionados a";
		mes "Alquimia, certo?";
		close;
	case 2:
		mes "[Gever Al Sharp]";
		mes "Do que voc� precisa?";
		mes "Os manuais geralmente custam 100,000 zeny. Mas h� v�rios manuais especiais que ir�o custar muito mais.";
		next;
		switch(select("Manual de Cria��o de Po��es:Manual de Cria��o de �lcool:Manual de Cria��o de Fogo Grego:Manual de Cria��o de Pote de �cido:Manual de Cria��o de Plantas:Manual de Cria��o de Esfera Marinha:Manual de Cria��o de Revestimentos:Manual de Cria��o de Po��es Condensadas:Cancelar")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[Gever Al Sharp]";
			mes "Pois bem...";
			mes "Volte caso voc� tenha";
			mes "nescessidade de comprar";
			mes "um manual de produ��o.";
			close;
		}
	case 3:
		mes "[Gever Al Sharp]";
		mes "Certo, ent�o";
		mes "tenha um bom dia.";
		close;
	}

S_SellManual:
	if (Zeny < getarg(1)) {
		mes "[Gever Al Sharp]";
		mes "Vejo que voc� n�o";
		mes "tem zeny suficiente.";
		mes "Verifique quanto voc�";
		mes "tem de zeny primeiro.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes ""+getitemname(getarg(0))+"?";
	mes "Isso vai custar "+getarg(1)+" zeny.";
	next;
	if (select("Comprar:Cancelar") == 1) {
		set Zeny, Zeny-getarg(1);
		getitem getarg(0),1;
		mes "[Gever Al Sharp]";
		mes "Obrigado pela";
		mes "sua fidelidade.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Volte novamente";
	mes "outra hora.";
	close;
}
