//===== Cronus Script ========================================
//= Quest Específica da Classe Gatuno
//===== Por: =================================================
//= Cardoso
//===== Versão Atual: ========================================
//= 1.3b CR / 1.3 eA
//===== Descrição: ===========================================
//= Quest para Gatunos
//===== Changelog: ===========================================
//= 1.0 Fully working
//= 1.1 optimized [Lupus]
//=	TODO: Test, Add correct misceffects numbers
//= 1.2 Fixed experience gains to match upcoming rate adjustments. [SinSloth]
//= 1.2b Traduzido e revisado [Cardoso]
//= 1.3 Fixed a few minor mistakes [Playtester]
//= 1.3b Revisado novamente, pré-otimizado [Keoy]
//	 Eu incluí os babys nas quests, se alguém tiver informações oficiais
//	se eles participam ou não, por favor informar no bugtracker
//============================================================

moc_ruins,66,164,4	script	Treinador de Gatunos#T	84,{
	mes "[Yierhan]";
	if(Class == Job_Novice)
	{
		mes "Humn...?";
		mes "Um aprendiz?";
		mes "Ainda pensando";
		mes "em qual classe";
		mes "irá escolher...";
		mes "Não é mesmo?";
		next;
		mes "[Yierhan]";
		mes "Escute, se você decidir se tornar um gatuno - uma ótima escolha";
		mes "Venha e fale comigo. Eu lhe mostro o caminho!";
		close;

	} else
	//if(Class == Job_Thief || Class == Job_Thief_High || Class == Job_Assassin || Class ==Job_Assassin_Cross || Class == Job_Rogue || Class == Job_Stalker)
	if ( (eaclass()&EAJ_BASEMASK) == EAJ_THIEF )
	{
		if( Class != Job_Thief )
		{
			if(tu_thief01 < 8)
			{
				mes "O que você está";
				mes "fazendo aqui?! Não há";
				mes "nada que eu possa lhe ensinar!";
				mes "Você está muito a minha frente!";
				next;
				mes "[Yierhan]";
				mes "Aproveitando... Você";
				mes "poderia me ensinar";
				mes "algumas coisas!";
				mes "Por favor! Preciso de novos golpes!";
				close;
			}
		}
		if(tu_thief01 == 0)
		{
			mes "Olá.";
			mes "Sou Yierhan.";
			mes "Eu sou o cara";
			mes "encarregado de treinar";
			mes "novos gatunos.";
			next;
			switch(select("Treino?:Treino? Agora?!"))
			{
			case 1:
				mes "[Yierhan]";
				mes "Isso, treino. Mas fique tranquilo é apenas uma precaução.";
				next;
				break;
			case 2:
				mes "[Yierhan]";
				mes "Agora?!";
				mes "Sim, agora!";
				mes "Mas se você não está preparad"+(Sex)?"o":"a"+" por alguma razão, acho que posso esperar.";
				close;
			}
			mes "[Yierhan]";
			mes "Ok, antes de tudo, Gatunos usam ataques físicos. Bom, a maioria. Há alguns que gostam de usar arcos. Mas todos gostamos de velocidade!";
			next;
			mes "[Yierhan]";
			mes "Então para ataques físicos, qual atributo aumenta o dano? Vamos, ";
			mes "você deve saber isso... Se você não pulou o treino básico";
			next;
			switch(select("^6B8E23INT^000000:^2F4F2FFOR^000000:^23238EDES^000000"))
			{
			case 1:
				mes "[Yierhan]";
				mes "Affee! ^6B8E23INT^000000 implica no dano mágico, defesa mágica e algumas habilidades. Gatunos não usam magia!";
				next;
				mes "[Yierhan]";
				mes "Você deve ter pulado o treinamento básico mesmo! Não que eu esteja criticando. De qualquer forma, é ^2F4F2FFOR^000000 que aumenta o dano físico, ok?";
				next;
				mes "[Yierhan]";
				mes "Se você achar que seu dano físico está muito fraquinho, tente colocar mais pontos ^2F4F2FFOR^000000.";
				set tu_thief01, 1;
				getexp 200,100;
				//misceffect EF_HIT5;
				close;
			case 2:
				mes "[Yierhan]";
				mes "Isso, está certo! Se você deseja aumentar o dano físico";
				mes "você deve colocar mais pontos em ^2F4F2FFOR^000000. Aumentando a força, aumentará";
				mes "sua capacidade de carregar itens também.";
				next;
				mes "[Yierhan]";
				mes "Se você achar que seu dano físico está muito fraquinho, tente colocar mais pontos em ^2F4F2FFOR^000000.";
				next;
				mes "[Yierhan]";
				mes "Claro, ^23238EDEs^000000 e SOR também podem";
				mes "aumentar seu dano físico, mas é muito pouco comparado com ^2F4F2FFOR^000000.";
				set tu_thief01, 1;
				getexp 400,200;
				//misceffect EF_HIT5;
				close;
			case 3:
				mes "[Yierhan]";
				mes "Affee! ^23238EDES^000000 implica na";
				mes "precisão dos ataques, não o dano! Bom, a não ser que esteja";
				mes "usando um arco. Se não for o caso ^23238EDES^000000 aumentará muito pouco o dano.";
				next;
				mes "[Yierhan]";
				mes "Você deve ter pulado o treinamento básico mesmo! Não que eu esteja criticando. De qualquer forma, é a ^2F4F2FFOR^000000 que aumenta o dano físico, ok?";
				next;
				mes "[Yierhan]";
				mes "Se você achar que seu dano físico está muito fraquinho, tente colocar mais pontos ^2F4F2FFOR^000000.";
				set tu_thief01, 1;
				getexp 200,100;
				//misceffect EF_HIT5;
				close;
			}
		} else if(tu_thief01 == 1)
		{
			mes "Ok, chega de falar sobre atributos.";
			mes "Quer saber? Eu acho que vou falar sobre as habilidades que nós gatunos usamos.";
			next;
			mes "[Yierhan]";
			mes "Mas antes tente evoluir um pouco suas habilidades";
			mes "e depois volte aqui.";
			set tu_thief01, 2;
			close;
		} else if(tu_thief01 == 2)
		{
			mes "Ok, deixe-me ver suas habilidades. Você sabia que dependendo das suas habilidades você deve mudar sua estratégia de batalha? Habilidades são tão importantes quanto atributos!";
			next;
			set @chk_th_skill1, getskilllv("TF_DOUBLE");
			set @chk_th_skill2, getskilllv("TF_MISS");
			set @chk_th_skill3, getskilllv("TF_STEAL");
			set @chk_th_skill4, getskilllv("TF_HIDING");
			set @chk_th_skill5, getskilllv("TF_POISON");
			set @chk_th_skill6, getskilllv("TF_DETOXIFY");

			if(@chk_th_skill1 == 0 && @chk_th_skill2 == 0 && @chk_th_skill3 == 0 && @chk_th_skill4 == 0 && @chk_th_skill5 == 0 && @chk_th_skill6 == 0){
				mes "[Yierhan]";
				mes "Você ainda não aprendeu nenhuma habilidade? Vamos lá, são suas habilidades que o diferenciam de todo mundo!";
				close;
			} else {
				if(@chk_th_skill1 > 0){
					mes "[Yierhan]";
					mes "Ah, então você aprendeu:";
					mes "O nível "+@chk_th_skill1+" de Ataque duplo.";
					mes "Ótimo! Essa habilidade lhe dá uma chance de atacar duas vezes o alvo";
					mes "ao invés de uma. É muito boa!";
					next;
					mes "[Yierhan]";
					mes "É uma habilidade passiva, por isso não usa SP e sempre está ativa.";
					next;
				}
				if(@chk_th_skill2 > 0){
					mes "[Yierhan]";
					mes "Ah, então você aprendeu:";
					mes "O nível "+@chk_th_skill2+" de Perícia em esquiva?";
					mes "Essa habilidade aumenta a chance de seus inimigos errarem o ataque.";
					next;
				}
				if(@chk_th_skill3 > 0){
					mes "[Yierhan]";
					mes "Uau, você conseguiu";
					mes "O nível "+@chk_th_skill3+" de Furto~";
					mes "É essa a habilidade que dá o nome de nossa classe! Porém não dá para";
					mes "usar nas pessoas, infelizmente...";
					next;
					mes "[Yierhan]";
					mes "Mas você pode roubar itens de monstros. Se você tiver sorte, pode conseguir um item bom.";
					next;
				}
				if(@chk_th_skill4 > 0){
					mes "[Yierhan]";
					mes "Você aprendeu:";
					mes "O nível "+@chk_th_skill4+" de Esconderijo?";
					next;
					mes "[Yierhan]";
					mes "Essa habilidade é muito útil,";
					mes "principalmente quando você está rodeado de inimigos fortes. Mas tome cuidado...";
					next;
					mes "[Yierhan]";
					mes "Alguns monstros ainda podem detecta-lo não importando o quanto tente se esconder, eles te acham!";
					next;
				}
				if(@chk_th_skill5 > 0){
					mes "[Yierhan]";
					mes "Ok, vejo que aprendeu";
					mes "O nível "+@chk_th_skill5+" de Envenenar.";
					mes "Você gosta de ser perigoso,";
					mes "não gosta?";
					next;
					mes "[Yierhan]";
					mes "Esse ataque tem a chance de envenenar";
					mes "seu inimigo por um curto periodo de tempo. Enquanto envenenado o inimigo perderá HP constantemente e ficará com a defesa fragilizada.";
					next;
					mes "[Yierhan]";
					mes "Heh, mas lembre-se.";
					mes "Se o monstro for muito mais forte que você será quase impossível envenena-lo. Então não dê uma de louco.";
					next;
				}
				if(@chk_th_skill6 > 0){
					mes "[Yierhan]";
					mes "O nível "+@chk_th_skill6+" Desintoxicar.";
					next;
					mes "[Yierhan]";
					mes "Você só pode aprender Desintoxicar";
					mes "depois de ter aprendido a habilidade Envenenar. Desintoxicar lhe permite retirar o envenenamento de certo alvo.";
					next;
				}
			}
			mes "[Yierhan]";
			mes "Ok, se você";
			mes "quiser saber sobre";
			mes "qualquer outra habilidade,";
			mes "eu posso lhe explicar rapidamente.";
			set tu_thief01, 3;
			getexp BaseLevel*30,BaseLevel*15;
			//misceffect EF_HIT5;//CHECK
			close;
		} else if(tu_thief01 == 3)
		{
			mes "Então...";
			mes "Há mais alguma";
			mes "habilidade que você";
			mes "quer que eu explique ou";
			mes "já tem toda informação";
			mes "que deseja?";
			next;
			while(1)
			{
				switch(select("Ataque Duplo:Perícia em esquiva:Furto:Esconderijo:Envenenar:Desintoxicar:Cancelar"))
				{
				case 1: 
					mes "[Yierhan]";
					mes "Como o nome diz,";
					mes "permite que você de dois ataques ao invés de um, com uma % de sucesso.";
					next;
					mes "[Yierhan]";
					mes "É uma habilidade passiva, por isso não usa SP e sempre está ativa.";
					set @read_d, 1;
					next;
					break;
				case 2:
					mes "[Yierhan]";
					mes "Perícia em esquiva";
					mes "aumenta sua taxa de";
					mes "esquiva que faz com";
					mes "que seus inimigos errem";
					mes "ataques contra você.";
					next;
					mes "[Yierhan]";
					mes "Assim como o ataque duplo, essa habilidade é passiva por isso não usa SP e sempre está ativa.";
					set @read_f, 1;
					next;
					break;
				case 3:
					mes "[Yierhan]";
					mes "Furto é uma habilidade ativa que dá a chance de roubar alguns itens de graça.";
					mes "Mas você só pode roubar itens de monstros. Infelizmente não funciona com humanos.";
					set @read_s, 1;
					next;
					break;
				case 4:
					mes "[Yierhan]";
					mes "Esconderijo é uma habilidade ativa, que permite que você se esconda no subsolo. Você não poderá se mexer enquanto essa habilidade estiver ativada.";
					next;
					mes "[Yierhan]";
					mes "Essa habilidade é muito útil,";
					mes "principalmente quando você está rodeado de inimigos fortes. Mas tome cuidado...";
					next;
					mes "[Yierhan]";
					mes "Alguns monstros ainda podem detecta-lo não importando o quando tente se esconder, eles te acham!";
					set @read_p, 1;
					next;
					break;
				case 5: 
					mes "[Yierhan]";
					mes "Envenenar é uma habilidade ativa e ofensiva, que todo gatuno deveria ter, na minha opinião...";
					next;
					mes "[Yierhan]";
					mes "Esse ataque tem a chance de";
					mes "envenenar seu inimigo por um periodo de tempo. Enquanto envenenado o inimigo perderá HP constantemente e ficará com a defesa fragilizada.";
					mes "Lembre-se disso.";
					set @read_h, 1;
					next;
					break;
				case 6: 
					mes "[Yierhan]";
					mes "Já que nós gatunos";
					mes "podemos envenar, é";
					mes "necessário saber como";
					mes "reverter o envenenamento,";
					mes "e é para isso que serve a habilidade Desintoxicar.";
					next;
					mes "[Yierhan]";
					mes "Você só pode aprender Desintoxicar";
					mes "depois de ter aprendido a habilidade Envenenar. Desintoxicar lhe permite retirar o envenenamento de certo alvo.";
					set @read_r, 1;
					next;
					//misceffect EF_DETOXICATION;
					next;
					break;
				case 7:
					mes "[Yierhan]";
					mes "Boa...";
					mes "Eu estou cansado de explicar todas essas habilidades mesmo. Vamos avançar para a";
					mes "próxima parte de nosso treinamento...";
					next;
					mes "[Yierhan]";
					mes "Agora que você é praticamente um Expert em habilidades, eu queria que você evoluísse um pouco as habilidades, ok?";
					set tu_thief01, 4;
					//misceffect EF_HIT5;
					if(@read_d){
						getexp 300,100;
					}
					if(@read_f){
						getexp 300,100;
					}
					if(@read_s){
						getexp 300,100;
					}
					if(@read_p){
						getexp 300,100;
					}
					if(@read_h){
						getexp 300,100;
					}
					if(@read_r){
						getexp 300,100;
					}
					close;
				}
			}
		} else if(tu_thief01 == 4){
			mes "Ok, nós estudamos as habilidades, você já práticou um pouco, não é? ...Espero que sim.";
			next;
			mes "[Yierhan]";
			mes "Ok, agora quero que você faça esse treinamento de luta. Está é a chance de experimentar qual habilidade é melhor em certas situações.";
			next;
			mes "[Yierhan]";
			mes "Vá e me traga";
			mes "10 ^ff0000"+getitemname(916)+"^000000.";
			mes "Você pode consiguir isso matando Pickys. Não vai ser difícil. Ah, e use essa asa para voltar aqui.";
			set tu_thief01, 5;
			savepoint "moc_ruins",80,164;
			getitem 602, 1;
			getexp 100,50;
			//misceffect EF_HIT5;
			close2;
			warp "moc_fild12",158,373;
			end;
		} else if(tu_thief01 == 5 || tu_thief01 == 6){
			if(countitem(916) < 10){	//Feather_Of_Birds
				mes "Ei, você não";
				mes "tem os 10 ^ff0000"+getitemname(916)+"^000000 que lhe pedi.";
				mes "Lembre-se que, experiência é o mais importante.";
				close2;
				warp "moc_fild07",203,38;
				end;
			} else {
				mes "Ah~ Agora sim.";
				mes "Bom trabalho, meu amigo.";
				mes "Essas penas são o símbolo de sua evolução.";
				next;
				mes "[Yierhan]";
				mes "Eu espero que continue dando o melhor de você.";
				mes "Você tem alguma dúvida?";
				next;
				if(tu_thief01 == 6){
					switch(select("A respeito dos vestigios.", "No.")){
					case 1:
						mes "[Yierhan]";
						mes "Ah....";
						mes "Você achou alguns vestigios, né?";
						mes "Hmmm...";
						mes "Eu não gostaria de lhe contar sobre isso,";
						mes "logo depois de ter se tornado um gatuno...mas...";
						next;
						mes "[Yierhan]";
						mes "Houve uma batalha ao sul da cidade.";
						mes "Era meio noite quando aconteceu, por isso";
						mes "apenas algumas pessoas sabem sobre isso.";
						mes "Eu fiquei acordado até mais tarde para uma reunião do clã,";
						mes "e sem querer testemunhei a batalha.";
						next;
						mes "[Yierhan]";
						mes "Depois eu fui até onde ocorreu a batalha.";
						mes "Tudo que achei foi aqueles vestigios indicando que a batalha continuou mais para o sul.";
						mes "Parece que esses vestigios tem a ver com alguns Assassinos...";
						mes "já que o vestigios são venenosos.";
						mes "Mas isso é somente minha suposição.";
						next;
						mes "[Yierhan]";
						mes "Se você estiver interessado, siga os vestigios.";
						mes "Mas não se esqueça, eles ainda são venenosos.";
						set tu_thief01, 8;
						getitem 1207, 1;
						getexp 1000,500;
						//misceffect EF_HIT5;
						close;
					case 2:
						mes "[Yierhan]";
						mes "Ah, tudo bem então.";
						mes "Espero ver você muito mais forte, futuramente.";
						mes "E por favor aceite isso.";
						mes "Hahaha...";
						set tu_thief01, 7;
						getitem 1207, 1;
						getexp 500,200;
						//misceffect EF_HIT5;
						close;
					}
				} else {
					switch(select("Foi um prazer lhe conhecer.", "Não."))
					{
					case 1:
						mes "[Yierhan]";
						mes "Hahaha...foi um prazer para mim também.";
						mes "Espero que de o melhor de sí sempre!";
						mes "Assim, você se tornará o mais poderoso gatuno de todos os tempos.";
						next;
						mes "[Yierhan]";
						mes "E por favor aceite isso.";
						mes "Hahaha...";
						break;
					case 2:
						mes "[Yierhan]";
						mes "Ah, tudo bem então.";
						mes "Espero ver você muito mais forte, futuramente.";
						mes "E por favor aceite isso.";
						mes "Hahaha...";
						break;
					}
					set tu_thief01, 7;
					getitem 1207, 1;
					getexp 500,200;
					close;
				}
			}
		} else if(tu_thief01 == 7){
			mes "Houve uma batalha ao sul da cidade.";
			mes "Era meio noite quando aconteceu, por isso";
			mes "apenas algumas sabem sobre isso.";
			mes "Eu fiquei acordado até mais tarde para uma reunião da guilda,";
			mes "e sem querer testemunhei a batalha.";
			next;
			mes "[Yierhan]";
			mes "Depois eu fui até onde ocorreu a batalha.";
			mes "Tudo que achei foi aqueles vestigios indicando que a batalha continuou mais para o sul.";
			mes "Parece que esses vestigios tem a ver com alguns Assassinos...";
			mes "já que o vestigios são venenosos.";
			mes "Mas isso é somente minha suposição.";
			next;
			mes "[Yierhan]";
			mes "Se você estiver interessado, siga os vestigios.";
			mes "Mas não se esqueça, eles ainda são venenosos.";
			next;
			mes "[Yierhan]";
			mes "Não se esqueça, erva verde e poção verde podem";
			mes "curar envenenamento. Se alguem";
			mes "aparecer com uma Gema Vermelha na sua frente, cuidado.";
			mes "Ele pode tentar te envenenar com a Gema.";
			set tu_thief01, 8;
			getexp 200,100;
			//misceffect EF_HIT5;
			close;
		} else if(tu_thief01 == 8){
			mes "Então, como você está?";
			mes "Lutando contra inimigos desconhecidos e correndo";
			mes "grande perigo como sempre?.";
			mes "Parece que em torno da cidade há alguns inimigos poderosos,";
			mes "portanto cuidado.";
			next;
			mes "[Yierhan]";
			mes "Quando estiver por ai, tome o máximo";
			mes "de cuidado. Não só monstros são seus inimigos,";
			mes "mas, pessoas também, até seus amigos.";
			close;
		} else if(tu_thief01 < 26){
			mes "Eu fiquei sabendo que um grande assassino";
			mes "nunca voltou de sua missão.";
			mes "Porem o Clã dos Assassinos divulgou que a missão foi concluída,";
			mes "mas ele nunca voltou para lá.";
			mes "E olha que ele era muito forte e competente...pelo que me recordo.";
			next;
			mes "[Yierhan]";
			mes "Portanto, tome cuidado, fique sempre atento.";
			close;
		} else if(tu_thief01 == 26){
			mes "Você terminou sua missão?";
			mes "Eu sei que ela não era fácil.";
			next;
			switch(select("Estou ainda investigando.:Ainda não.:Sim, terminei.")){
			case 1:
				mes "[Yierhan]";
				mes "Ah...";
				mes "Entendo. Ela não é simples de ser terminada.";
				mes "Continue fazendo um bom trabalho.";
				close;
			case 2:
				mes "[Yierhan]";
				mes "Ah...";
				mes "Entendo. Ela não é simples de ser terminada.";
				mes "Continue fazendo um bom trabalho.";
				close;
			case 3:
				mes "[Yierhan]";
				mes "Ohhhh, você conseguiu!";
				mes "Ok, está tudo certo.";
				mes "Estava esperando por muito tempo esse dia chegar.";
				next;
				mes "- Você informou a ele sobre sua investigação -";
				mes "- e sobre o pedaço de pano adquirido em sua missão. -";
				next;
				mes "[Yierhan]";
				mes "Hmm...";
				mes "Deixe-me ver o pano.";
				next;
				mes "- Você deu a ele o pedaço de pano. -";
				next;
				mes "[Yierhan]";
				mes "HUmm...";
				mes "Você fez...";
				mes "Você fez um ótimo trabalho.";
				mes "Você me trouxe informações muito impotantes.";
				next;
				mes "[Yierhan]";
				mes "Como você pode ver, a textura desse pano";
				mes "é peculiar de assassinos.";
				mes "E essa textura particularmente é";
				mes "usada por Algozes.";
				next;
				mes "[Yierhan]";
				mes "Com isso, eu posso descubrir";
				mes "o que aconteceu aquela noite.";
				mes "Obrigado.";
				next;
				mes "[Yierhan]";
				mes "Vou perguntar para outros integrantes mais sábio o clã para cuidar";
				mes "da investigação.";
				mes "Você fez perfeitamente o trabalho, agora quero";
				mes "que você foque o seu treinamento.";
				next;
				mes "[Yierhan]";
				mes "Por favor aceite isso.";
				mes "É uma recompença pelo seu ótimo trabalho.";
				set tu_thief01, 27;
				getitem 2307, 1;
				set Zeny, Zeny + 5000;
				getexp 8000,3000;
				next;
				mes "[Yierhan]";
				mes "Tome cuidado agora.";
				close;
			}
		} else {
			mes "Sempre busque seu sonho.";
			mes "A vida sem um sonho não é uma vida perfeita.";
			close2;
		}
	//} else if(Class == Job_Mage || Class == Job_Mage_High || Class == Job_Wizard || Class == Job_High_Wizard || Class == Job_Sage || Class == Job_Professor){
	} else if ( (eaclass()&EAJ_BASEMASK) == EAJ_MAGE )
	{
		if(tu_magician01 < 7)
		{
			mes "Sou um treinador de gatunos.";
			mes "E pelo visto você não é um.";
			mes "Você deve ser gatuno se quiser";
			mes "ter um professor como eu...hahaha.";
			close;
		} else if(tu_magician01 == 7)
		{
			mes "Hm...?";
			mes "Estranho, apenas gatunos andam por aqui. O que alguem como você faz por aqui? A não ser que...";
			next;
			switch(select("Estou aqui em nome de 'Mana.'"))
			{
			case 1:
				mes "[Yierhan]";
				mes "Ah, estava esperando por você. Bom, eu pedi a Mana que me ajudasse com uma coisa.";
				next;
				mes "[Yierhan]";
				mes "Você veio na hora certa. Sabe, algo muito estranho foi achado ao sul de Morroc.";
				next;
				mes "[Yierhan]";
				mes "Basicamente, achamos rastros de veneno usado em uma batalha. Nós iamos investigar, mas temos coisas mais importantes a fazer.";
				next;
				mes "[Yierhan]";
				mes "Foi por isso que pedi uma ajuda ao Clã dos Magos. Felizmente, tenho amizade com Mana...";
				next;
				mes "[Yierhan]";
				mes "Então, por favor se dirija para o sul de morroc, lá é o melhor local para começar a investigação. Você logo verá o que achamos logo que sair de Morroc.";
				set tu_magician01, 8;
				break;
			}
		} else if(tu_magician01 < 26){
			mes "Há histórias que dizem que um Grande Assassino desapareceu em uma missão...";
			next;
			mes "[Yierhan]";
			mes "O que é estranho sobre isso, é que a Guilda dos Assassinos informou que a missão foi concluída.";
			next;
			mes "[Yierhan]";
			mes "Tome cuidado, amigo. Parece que sua missão tem a ver com esse misterioso assassino...";
			close;
		} else if(tu_magician01 == 26){
			mes "Hmm.....";
			mes "Então como vai indo?";
			mes "Estou tão ocupado com vários trabalhos ultimamente";
			mes "que quase não tenho tempo para investigar.";
			next;
			switch(select("Eu ainda estou investigando.:Ainda nada.:Terminei de investigar.")){
			case 1:
				mes "[Yierhan]";
				mes "Ah...";
				mes "Entendo. Ela não é simples de ser terminada.";
				mes "Continue fazendo um bom trabalho.";
				close2;
				break;
			case 2:
				mes "[Yierhan]";
				mes "Ah...";
				mes "Entendo. Ela não é simples de ser terminada.";
				mes "Continue tentando.";
				close2;
				break;
			case 3:
				mes "[Yierhan]";
				mes "Ohhhh, você conseguiu!";
				mes "Ok, está tudo certo.";
				mes "Estava esperando por muito tempo esse dia chegar.";
				next;
				mes "- Você informou ele sobre sua investigação -";
				mes "- e sobre o pedaço de pano adquirido em sua missão. -";
				next;
				mes "[Yierhan]";
				mes "Como você pode ver, a textura desse pano";
				mes "é peculiar de assassinos.";
				mes "E essa textura particularmente é";
				mes "usada por Algozes.";
				next;
				mes "[Yierhan]";
				mes "Com isso, eu posso descubrir";
				mes "o que aconteceu aquela noite.";
				mes "Obrigado.";
				next;
				mes "[Yierhan]";
				mes "Ah, aceite isso como forma de gratidão.";
				mes "Mandarei uma mensagem à Mana falando sobre sua ajuda.";
				mes "Se cuide.";
				set tu_magician01, 27;
				set Zeny, Zeny + 5000;
				getexp 5000,2000;
				close2;
				break;
			}
		} else {
			mes "Como disse, não foi apenas um grupo mas no mínimo";
			mes "dois ou até três grupos envolvidos com isso.";
			mes "Hmm...Tenho a sensaçao que algo ruim acontecerá em breve.";
			close;
		}
	}
	mes "A coisa mais romantica ";
	mes "no deserto são com certeza";
	mes "as tempestades de areia. Hahaha.";
	close;
}