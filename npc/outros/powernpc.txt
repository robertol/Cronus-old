//===== Cronus Script ========================================
//= Power Arena de Kunlun
//===== Por: =================================================
//= KarLaeda
//===== Versão Atual: ========================================
//= 1.1b
//===== Comentários Adicionais: ==============================
//= 1.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.1a Traduzido. [FrozenCirius]
//= 1.1b Revisado. [RoM]
//============================================================

gonryun,180,118,4	script	Homem de Ferro#gnp	85,{
	if (countitem(1201) > 0) {
		mes "- Um momento !!! -";
		mes "- Você está carregando -";
		mes "- itens demais. -";
		mes "- Por Favor volte -";
		mes "- depois que você estiver um pouco mais leve. -";
		close;
	}
	if (BaseJob == Job_Novice) {
		mes "[Songmoodoo]";
		mes "Crianças não são permitidas aqui.";
		close;
	}
	if (in_battle == 3) set in_battle,1;
	if (in_battle < 1) {
		mes "[Songmoodoo]";
		mes "Hey hey~ Isso não é algo";
		mes "que se vê todos os dias.";
		mes "Oh~ você parece forte!";
		mes "Quer tentar?";
		next;
		switch(select("Claro, porque não?:?????:Não, obrigado.")){
		case 1:
			mes "[Songmoodoo]";
			mes "Haha~ Eu sabia que você iria tentar.";
			mes "Se você conseguir levantar aquilo,";
			mes "Eu lhe direi algo realmente interessante.";
			mes "Pronto?";
			next;
			mes "^0000FFSongmoodoo aponta para uma rocha";
			mes "e te pede para levantá-la.";
			if (BaseJob < Job_Knight) {
				if (checkweight(7049,690)) {
					mes "^0000FFVocê levantou a rocha levemente.^000000";
					set in_battle,1;
					getitem 7049,690; // Pedra
					next;
					emotion e_no1;
					mes "[Songmoodoo]";
					mes "Wow~ Excelente~";
					mes "Te levarei para um lugar legal.";
					mes "Te vejo da próxima vez.";
					mes "Até mais~";
				} else {
					mes "Parece muito pesado para você.^000000";
					next;
					mes "[Songmoodoo]";
					mes "Você ainda precisa de muito treino.";
					mes "Volte quando tiver mais prática.";
				}
			} else if (BaseClass == Job_Swordman || BaseClass == Job_Thief || BaseClass == Job_Merchant || BaseJob == Job_Monk) {
				if (checkweight(7049,1100)) {
					mes "^0000FFVocê levantou a pedra levemente.^000000";
					set in_battle,1;
					getitem 7049,1100; // Pedra
					next;
					emotion e_no1;
					mes "[Songmoodoo]";
					mes "Wow~ Excelente~";
					mes "Te levarei para um lugar legal.";
					mes "Te vejo da próxima vez.";
					mes "Até mais~";
				} else {
					mes "Parece muito pesado para levantar.^000000";
					next;
					mes "[Songmoodoo]";
					mes "Você ainda precisa de muito treino.";
					mes "Volte quando tiver mais prática.";
				}
			} else {
				if (checkweight(7049,730)) {
					mes "^0000FFVocê levantou a rocha levemente.^000000";
					set in_battle,1;
					getitem 7049,730; // Pedra
					next;
					emotion e_no1;
					mes "[Songmoodoo]";
					mes "Wow~ Excelente~";
					mes "Te levarei para um lugar legal.";
					mes "Te vejo da próxima vez.";
					mes "Até mais~";
				} else {
					mes "Parece pesado demais para levantar.^000000";
					next;
					mes "[Songmoodoo]";
					mes "Você ainda precisa de muito treino.";
					mes "Volte quando tiver mais prática.";
				}
			}
			break;
		case 2:
			mes "[Songmoodoo]";
			mes "Talvez não seja uma boa idéia.";
			mes "Mas se você acredita ser forte,";
			mes "essa pode ser uma grande opotunidade de tentar";
			mes "algo novo. Quer tentar?";
			break;
		case 3:
			mes "[Songmoodoo]";
			mes "Eu também acho que não...";
			mes "...";
			emotion e_pif;
			break;
		}
	} else if (in_battle == 1) {
		mes "[Songmoodoo]";
		mes "É bom te ver de novo!";
		mes "Vamos lá?";
		next;
		if (select("Claro:Talvez na próxima vez") == 1) {
			mes "[Songmoodoo]";
			mes "Excelente! Haha.";
			mes "Eu gosto da sua confiança.";
			mes "Boa sorte pra você~";
			close2;
			warp "gon_test",53,6;
			end;
		} else {
			mes "[Songmoodoo]";
			mes "Erm...";
			mes "Tudo bem.";
			mes "Então te vejo na próxima vez.";
		}
	} else {
		mes "[Songmoodoo]";
		mes "Você correu de lá?";
		mes "Acha que não é forte o suficiente?";
		mes "Eu lhe darei outra chance, se desejar.";
		mes "Até mais.";
		emotion e_pif;
		set in_battle,1;
	}
	close;
}

gon_test,50,14,4	script	Administrador#gnp	780,{
	mes "[Administrator]";
	if (BaseJob == Job_Novice) {
		mes "Crianças não são bem-vindas aqui.";
		close;
	}
	if ($@in_battle) {
		mes "Desculpe, o Campo de Batalha";
		mes "está ocupado agora.";
		mes "Tente de novo mais tarde.";
		close;
	}
	mes "Você está pronto?";
	mes "";
	mes "Lembre que você tem de me pagar";
	mes "500 zeny para batalhar.";
	next;
	if (select("Sim, deixe-me batalhar!:Um momento, por favor.") == 2) {
		mes "[Administrator]";
		mes "Ok, te vejo mais tarde.";
		close;
	}
	if (Zeny < 500) {
		mes "[Administrator]";
		mes "Desculpe, mas você não possui 500 zeny.";
		close;
	}
	set Zeny,Zeny - 500;
	set in_battle,1;
	warp "gon_test",42,86;
	set $@in_battle,1;
	setnpctimer 0,"Invocador#gnp";
	startnpctimer "Invocador#gnp";
	end;
}

gon_test,42,89,4	script	Invocador#gnp	774,{
	mes "[SongYeunWoo]";
	if (in_battle == 3) {
		mes "Por favor, volte depois de se registrar.";
		close;
	} else if (in_battle == 2) {
		mes "Você já tem uma partida para disputar.";
		mes "Você só pode disputar uma partida por vez.";
		mes "Por favor, retorne se você quiser uma partida";
		mes "com outros monstros.";
		close;
	}
	mes "Bem-vindo.";
	mes "Quais monstros você gostaria de invocar?";
	next;
	set @gnpGroup,select("Grupo 1:Grupo 2:Grupo 3:Grupo 4:Grupo 5:Grupo 6:Grupo 7:Grupo 8:Grupo 9:Grupo 10:Grupo 11");
	set @gnpGroupMob$,.gnpMobsName$[(@gnpGroup-1)*6];
	for (set @i,1; @i < 6; set @i, @i+1) {
		set @gnpGroupMob$, @gnpGroupMob$ + ":" + .gnpMobsName$[(@gnpGroup-1)*6 + @i];
	}
	set @gnpMob,select(@gnpGroupMob$);
	mes "[SongYeunWoo]";
	mes "Que comecem as batalhas!";
	close2;
	set in_battle,2;
	if (getnpctimer(0) < 180000) monster "gon_test",56,86,.gnpMobsName$[(@gnpGroup-1)*6 + @gnpMob - 1],.gnpMobsId[(@gnpGroup-1)*6 + @gnpMob - 1],1,"Invocador#gnp::OnGnpMobDead";
//  do init timer on mob select ?
//	setnpctimer 0;
	end;

	OnInit:
		initnpctimer;
		stopnpctimer;
		setarray .gnpMobsName$[0],"Familiar","Esporo Venenoso","Drainliar","Argos","Louva-a-Deus","Lobo do Deserto";
		setarray .gnpMobsId[0],1419,1428,1434,1430,1457,1432;
		setarray .gnpMobsName$[6],"Orc Esqueleto","Sasquatch","Esqueleto Operário","Matyr","Obeaune","Bongun";
		setarray .gnpMobsId[6],1462,1442,1469,1460,1425,1472;
		setarray .gnpMobsName$[12],"Trilobita","Kobold Arqueiro","Soldadinho de Chumbo","Cavalo Marinho","Jakk","Carniçal";
		setarray .gnpMobsId[12],1454,1455,1443,1426,1436,1423;
		setarray .gnpMobsName$[18],"Marduk","Talo de Verme","Argiope","Marionete","Chepet","Mosca Caçadora";
		setarray .gnpMobsId[18],1458,1440,1429,1459,1444,1422;
		setarray .gnpMobsName$[24],"Ísis","Punk","Sorrateiro","Petite Terrestre","Petite Voador","Deviruchi";
		setarray .gnpMobsId[24],1421,1481,1424,1465,1466,1433;
		setarray .gnpMobsName$[30],"Pesadelo","Orc Arqueiro","Bafomé Jr.","Injustiçado","Mímico","Nove-Caudas";
		setarray .gnpMobsId[30],1427,1473,1431,1446,1474,1471;
		setarray .gnpMobsName$[36],"Xamã do Vento","Grand Orc","Minorous","Raydric","Raydric Arqueiro","Esqueleto Prisioneiro";
		setarray .gnpMobsId[36],1450,1439,1461,1467,1453,1479;
		setarray .gnpMobsName$[42],"Tritão","Alma Penada","Joker","Penomena","Alarme","Druida Maligno";
		setarray .gnpMobsId[42],1451,1475,1437,1441,1476,1435;
		setarray .gnpMobsName$[48],"Arclouse","Moldura Maldita","Rideword","Sting","Anolian","Khalitzburg";
		setarray .gnpMobsId[48],1477,1448,1478,1489,1488,1438;
		setarray .gnpMobsName$[54],"Jirtas","Pasana","Executor","Dragão Mutante","Quimera","Tirfing";
		setarray .gnpMobsId[54],1482,1464,1487,1449,1456,1486;
		setarray .gnpMobsName$[60],"Grifo","Rybio","Andarilho","Fendark","Mysteltainn";
		setarray .gnpMobsId[60],1447,1483,1490,1484,1485;
	end;

	OnTimer120000:
		areaannounce "gon_test",41,81,74,92,"1 min. restante",0;
	end;

	OnTimer180000:
		killmonster "gon_test","Invocador#gnp::OnGnpMobDead";
	end;

	OnTimer182000:
		areaannounce "gon_test",41,81,74,92,"Acabou o tempo.",0;
	end;

	OnGnpMobDead:
		setnpctimer 0;
		set in_battle,1;
		areaannounce "gon_test",41,81,74,92,"Obrigado. Volte mais vezes.",0;
		sleep 4000;
	OnTimer184000:
		stopnpctimer;
		areawarp "gon_test",41,81,74,92,"gon_test",44,4;
		set $@in_battle,0;
	end;
}

gon_test,46,14,3	script	Guia do Campo#gnp	770,{
	mes "[SongHeeYeon]";
	mes ".....";
	mes "Olá~";
	mes "Esse é o Campo de Batalha.";
	mes "Tem alguma pergunta?";
	next;
	switch(select("Um Campo de Batalha?:Apenas um minutinho, senhora.:Entrar.:Sair.:Não tenho perguntas.")){
	case 1:
		mes "[SongHeeYeon]";
		mes "Como o nome sugere,";
		mes "aqui é um campo para partidas.";
		mes "Nós temos variados monstros";
		mes "em diferentes níveis.";
		next;
		mes "[SongHeeYeon]";
		mes "Monstros-Chefes de primeira classe ainda estão em preparativos.";
		mes "Nós cobramos uma pequena taxa pra entrar.";
		break;
	case 2:
		mes "[SongHeeYeon]";
		mes "Erm... Desculpe-me?";
		mes "Ah... Acho que tenho muito trabalho ainda...";
		mes "Sinto muito...";
		break;
	case 3:
		mes "[SongHeeYeon]";
		mes "Sim, obrigado.";
		mes "Divirta-se.";
		close2;
		set in_battle,3;
		warp "gon_test",25,98;
		end;
	case 4:
		mes "[SongHeeYeon]";
		mes "Obrigado.";
		mes "Por favor, volte sempre.";
		close2;
		warp "gonryun",177,112;
		end;
	case 5:
		mes "[SongHeeYeon]";
		mes "...";
		mes "Adeus.";
		break;
	}
	close;
}

gon_test,70,103,3	script	ChowAnAn#gnp	773,{
	mes "[ChowAnAn]";
	mes "Deseja voltar?";
	next;
	if (select("Sim.:Não.") == 1) {
		mes "[ChowAnAn]";
		mes "Obrigado.";
		mes "Por favor, volte mais vezes.";
		close2;
		set in_battle,1;
		warp "gon_test",44,4;
		end;
	}
	mes "[ChowAnAn]";
	mes "Obrigado.";
	close;
}
