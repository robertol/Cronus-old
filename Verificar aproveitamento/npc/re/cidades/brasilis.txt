//===== Hercules Script ======================================
//= Brasilis Town
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Brasilis Town Script
//===== Additional Comments: ================================= 
//= 1.0 First version. Transportation and Basic NPCs.
//= 1.1 Fixed Zeny not being removed for payment. [Kisuka]
//= 1.2 Updated dialog, added Ice Cream Maker. [L0ne_W0lf]
//= 1.3 Moved Alberta NPC to pre-re/re paths. [Euphy]
//============================================================ 

/* Pre-Renewal coordinates: alberta,247,115,3 */
alberta,246,82,3	script	Crewman#bra2	100,{
	mes "[Crewman]";
	mes "Ei, você já ouviu falar de um lugar chamado Brasilis?";
	mes "É uma cidade tropical que é tão quente quanto o deserto entretanto bastante chuvosa. É um lugar muito misterioso.";
	next;
	mes "[Crewman]";
	mes "Recentemente, encontramos uma nova rota oceânica para chegar lá fácilmente!";
	mes "A viagem custa a bagatela de 10,000 zeny! Então, gostaria de embarcar?";
	next;
	switch(select("Leve-me até Brasilis!:Ficarei aqui.")) {
	case 1:
		if (Zeny > 9999) {
			mes "[Crewman]";
			mes "Legal~!! Vamos partir~!";
			set zeny,zeny-10000;
			close2;
			warp "brasilis",314,60;
			end;
		}
		else {
			mes "[Crewman]";
			mes "Eu disse 10,000 zeny.";
			close;
		}
	case 2:
		mes "[Crewman]";
		mes "Bom, se mudar de ideia, diga-me. Estarei sempre à disposição.";
		close;
	}
}

brasilis,316,57,3	script	Crewman#bra1	100,{
	mes "[Crewman]";
	mes "Meu navio está partindo de volta para Alberta, gostaria de se juntar a nós?";
	next;
	switch(select("Voltar para Alberta.:Ainda não~.")) {
	case 1:
		mes "[Crewman]";
		mes "Sinto falta do meu lar.";
		close2;
		if (checkre(0))
			warp "alberta",243,82;
		else
			warp "alberta",244,115;
		end;
	case 2:
		mes "[Crewman]";
		mes "Ok, vá se preparar. Nos veremos quando retornar.";
		close;
	}
}

brasilis,155,165,3	script	Signpost#bra1	858,{
	mes ":: Museu de Arte ::";
	close;
}

brasilis,195,231,3	script	Signpost#bra2	858,{
	mes ":: Monumento de Verass ::";
	close;
}

brasilis,240,247,3	script	Signpost#bra3	858,{
	mes ":: Mercado ::";
	mes " ";
	mes "- Para suas Poções e Armamento -";
	close;
}

brasilis,303,309,3	script	Signpost#bra4	858,{
	mes ":: Corda da Selva ::";
	mes "";
	mes "- Não recomendado para pessoas cardíacias -";
	close;
}

brasilis,278,137,3	script	Signpost#bra5	858,{
	mes ":: Hotel de Brasilis ::";
	close;
}

brasilis,137,77,5	script	Vendedor de Sorvetes	85,{
	mes "[Vendedor de Sorvetes]";
	mes "Venha~venha~";
	mes "Sorvete é o aperitivo perfeito para um dia quente~";
	mes "Custa somente ^3355FF100 Zeny^000000~";
	mes "Sorvetes~";
	mes "Peguem seus sorvetes!";
	next;
	switch(select("Dê-me um!:Sorvete?:Cancelar.")) {
	case 1:
		mes "[Vendedor de Sorvetes]";
		mes "Já que há muitas pessoas querendo um delicioso sorvete, você poderá comprar apenas 5 de cada vez.";
		mes "Então, quantos irá querer?";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[Vendedor de Sorvetes]";
				mes "Nenhum?";
				mes "Certo, saia do caminho. Tenho clientes para atender.";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[Vendedor de Sorvetes]";
				mes "Uau.";
				mes "Você pediu demais.";
				mes "Se comer mais que 5, poderá ter uma indigestão. Muita calma nessa hora.";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[Vendedor de Sorvetes]";
			mes "Cara~! Você não tem dinheiro suficiente.";
			mes "São somente ^3355FF100 Zeny^000000~ Sério!";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[Vendedor de Sorvetes]";
			mes "Parece que você carrega muitas coisas.";
			mes "Alivie um pouco sua bagagem antes de comprar isto.";
			close;
		}
		set zeny,zeny-.@icecream_hap;
		getitem 536,.@input; //Ice_Cream
		close;
	case 2:
		mes "[Vendedor de Sorvetes]";
		mes "'Sorvete é...";
		mes "Espera.. Você não sabe..";
		mes "O que é um sorvete?";
		mes "Em que caverna..";
		mes "Você têm vivido?";
		next;
		mes "[Vendedor de Sorvetes]";
		mes "Não vou nem começar a dizer o quanto isso é esquisito.";
		mes "De qualquer forma, pegue seu sorvete bem aqui enquanto está bom e geladinho.";
		close;
	case 3:
		mes "[Vendedor de Sorvetes]";
		mes "Não perca a chance de tomar o melhor sorvete do mundo~!";
		close;
	}
}
