//===== rAthena Script ======================================= 
//= Quest NPCs related to Geffen
//===== By: ================================================== 
//= Dojo
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: =====================================
//= rAthena 7.15 +
//===== Description: =========================================
//= [Conversão do Aegis]
//= Welding Mask and Headset Quests.
//===== Additional Comments: ================================= 
//= 1.0 Primeira versão. [Dojo]
//============================================================

//==============================================================================================
// Quest da Máscara de Solda
//==============================================================================================
geffen_in,144,166,4	script	Ferreiro	63,{
	if (checkweight(1201,1) == 0 ) {
		mes "- Ei, espere aí !! -";
		mes "- Você não pode receber mais itens -";
		mes "Porque está carregando muitos itens. -";
		mes "- Por favor, alivie um pouco a sua carga e tente de novo - ";
		close;
	}
	mes "[Ferreiro]";
	mes "Sou o melhor Ferreiro que existe! Sabia?";
	mes "Trabalho há 30 anos neste fogo do inferno insuportável!";
	mes "Sou um Ferreiro há muitos anos, eu inventei algo incrível. Quer saber o que é?";
	next;
	if(select("Sim:Acho que não me ajudaria muito...") == 2) {
		mes "[Ferreiro]";
		mes "Deixe pra lá... Não vou perder meu tempo falando com você! Vá embora!";
		close;
	}
	mes "[Ferreiro]";
	mes "Como ferreiros, nós derretemos produtos metálicos a uma alta temperatura.";
	mes "Assim temos que encarar o calor e metais líquidos toda hora, nossos rostos vivem sempre queimados e machucados...";
	mes "Mas com esse item... Você não precisa se preocupar. Te apresento ^3355FF'Máscara de Solda'^000000 !!";
	next;
	mes "[Ferreiro]";
	mes "Protege seu rosto do calor.";
	mes "Este é o item mais procurado pelas pessoas!";
	mes "Que tal? Se me der ^2244FF'50 Aços'^000000 e ^4422FF2000 Zeny^000000, Farei uma para você imediatamente!";
	next;
	if(select("Nada mal! Eu quero uma.:Não me parece muito útil.") == 2) {
		mes "[Ferreiro]";
		mes "Uh-uh... Não sabe o que está perdendo!";
		mes "Meu jovem, você deve cuidar de seu rosto antes que ele fique cheio de rugas.";
		close;
	}
	if ((countitem(999) > 49) && (Zeny > 1999)) {
		mes "[Ferreiro]";
		mes "Ótimo! Farei agora mesmo!!";
		next;
		delitem 999,50; //Steel
		set Zeny,Zeny - 2000;
		getitem 2292,1; //Welding_Mask
		mes "[Ferreiro]";
		mes "Agora pode trabalhar no calor!";
		mes "Tchau...";
		close;
	}
	else {
		mes "[Ferreiro]";
		mes "Olha! Não posso fazer se você não tiver aço e zeny o suficiente!";
		mes "Você entende de alquimia? É o que você precisa!";
		mes "Ache aço e zeny para poder fazer alquimia.";
		close;
	}
}

//=============================================================================================
// Quest do Fone de Ouvido
//=============================================================================================
geffen_in,30,71,4	script	Zenhizha	83,{
	if (checkweight(1201,1) == 0 ) {
		mes "- Ei, espere aí !! -";
		mes "- Você não pode receber mais itens -";
		mes "Porque está carregando muitos itens. -";
		mes "- Por favor, alivie um pouco a sua carga e tente de novo - ";
		close;
	}
	mes "[Zenhizha]";
	mes "Quer ouvir a história do meu avô? É sobre a fabricação de Fones de Ouvido";
	next;
	switch(select("Continuar falando:De que item precisa?:Tente fabricá-lo:Cancelar")) {
	case 1:
		mes "[Zenhizha]";
		mes "Ele morreu há alguns anos...";
		mes "Ele foi muito importante para mim...";
		mes "Mais importante que meu pai.";
		mes "Ainda sinto como se ele nunca tivesse me deixado.";
		next;
		mes "[Zenhizha]";
		mes "Sempre gostei de sua sala de estudos.";
		mes "É como um Xanadu.";
		mes "Sempre que pensava nele...";
		mes "Eu iria lá ler alguns livros.";
		next;
		mes "[Zenhizha]";
		mes "Lá...";
		mes "Posso aprender tanto daqueles livros...";
		mes "Meu avô adorava alquimia e música...";
		mes "Há muitos livros sobre isso...";
		next;
		mes "[Zenhizha]";
		mes "Certo dia...";
		mes "Fui lá como de costume.";
		mes "E achei um rascunho por acidente.";
		next;
		mes "[Zenhizha]";
		mes "Neste rascunho há algo que parece com um Aquecedor de Orelha.";
		mes "Ao colocá-lo, dá pra se escutar vozes...";
		mes "Elas parecem vir de um músico.";
		mes "Como se tivesse uma orquestra dentro do Aquecedor de Orelha.";
		next;
		mes "[Zenhizha]";
		mes "Eu quero realizar o último desejo de meu avô...";
		mes "Produzi-lo!";
		mes "Mostrei este rascunho a outros alquimistas.";
		mes "Eu pedi para eles fabricarem! Mas nenhum deles consegue entender como funciona.";
		next;
		mes "[Zenhizha]";
		mes "Por isso, eu mesmo andei estudando...";
		mes "Já faz anos...";
		mes "Finalmente consegui entender.";
		mes "Só me resta achar o item para fazê-lo.";
		next;
		mes "[Zenhizha]";
		mes "Se tivesse os itens...";
		mes "Poderia começar prontamente...";
		mes "Se.. estiver interessado...";
		mes "Poderia me ajudar a achar esses itens?";
		next;
		mes "[Zenhizha]";
		mes "Só quero realizar seu último desejo.";
		mes "Criar o Fone de Ouvido do rascunho.";
		mes "Posso dá-lo a você quando terminar.";
		close;
	case 2:
		mes "[Zenhizha]";
		mes "Precisarei de ^3355FFAços^000000 ^FF555540^000000,";
		mes "E ^3355FFOridecon^000000 ^FF55551^000000,";
		mes "E ^3355FFÁlcool^000000 ^FF55551^000000,";
		mes "E ^3355FFCarvão^000000 ^FF55551^000000.";
		mes "Preciso de tudo isso.";
		close;
	case 3:
		if ((countitem(999) > 39) && (countitem(984) > 0) && (countitem(970) > 0) && (countitem(1003) > 0)) {
			delitem 999,40; //Steel
			delitem 984,1; //Oridecon
			delitem 970,1; //Alcohol
			delitem 1003,1; //Coal
			getitem 5001,1; //Headset
			mes "[Zenhizha]";
			mes "Tap! Tap! Tap! ...";
			next;
			mes "[Zenhizha]";
			mes "Muito obrigado!";
			mes "Por sua causa, pude realizar o desejo de meu avô.";
			mes "Ele ficará feliz se puder ver do céu.";
			next;
			mes "[Zenhizha]";
			mes "Como disse antes. Pode levá-lo!";
			mes "Estou tão feliz de poder construí-lo.";
			close;
		}
		else {
			mes "[Zenhizha]";
			mes "Obrigado pela gentileza... mas não há itens o suficiente aqui.";
			mes "Volte quando tiver todos!";
			close;
		}
	case 4:
		mes "[Zenhizha]";
		mes "Tchau!";
		close;
	}
}