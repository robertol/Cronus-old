//===== Cronus Script ========================================
//= NPCs referentes a Batalha Campal 
//===== Por: =================================================
//= L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.3 
//===== Compatível com: ======================================
//= eAthena; rAthena; Cronus; brAthena; Maya SVN. 
//===== Descrição: ===========================================
//= NPCs referentes a Batalha Campal 
//===== Comentários Adicionais: ==============================
//= 1.3 Traduzido do rA conforme os diálogos do bRO. [Evanio] 
//============================================================

// Generais
//============================================================
bat_room,161,158,3	script	Pajem de Guillaume#01	419,{ end; }
bat_room,161,160,3	script	Pajem de Guillaume#03	419,{ end; }

bat_room,160,141,3	script	Príncipe Croix	416,{
	cutin "bat_crua1",2;
	mes "[Príncipe Croix]";
	mes "Sábio aventureiro, por que não nos empresta seu poder para a vitória?";
	next;
	switch(select("Qual é o motivo da batalha?:Fale-me do General Guillaume")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[Príncipe Croix]";
		mes "O grande rei de Maroll, Marcel Marollo VII, anda muito doente.";
		mes "Sua Majestade declarou que deixará o futuro de Maroll nas minhas mãos ou nas do 3o. príncipe, General Guillaume.";
		next;
		mes "[Príncipe Croix]";
		mes "O General Guillaume pode ter vantagem nesta batalha, já que é o grande general de Maroll, mas isso não quer dizer que fatalmente ele vá vencer.";
		mes "Quero vencer esta batalha para poder trazer o povo de Maroll. eles já sofreram demais com a guerra...";
		next;
		switch(select("Sim, quero me juntar a você.:Terminar Conversa")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Príncipe Croix]";
			mes "Muito obrigado. Sinto que posso vencer com a ajuda de aventureiros como você.";
			mes "Agora, por favor desça as escadas e se junte aos seus camaradas que estão se preparando para lutar contra o inimigo.";
			break;
		case 2:
			mes "[Príncipe Croix]";
			mes "Por Maroll!";
			break;
		}
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[Príncipe Croix]";
		mes "O 3o. Príncipe, Guillaume, é o grande general de Maroll.";
		mes "Não preciso perder tempo lhe explicando o quanto ele é um grande líder e chefe de guerra, já que ele comanda o grande poder militar de Maroll.";
		next;
		mes "[Príncipe Croix]";
		mes "Infelizmente, há algo que ele e seus seguidores não percebem:";
		mes "O povo de Maroll quer mesmo gastar dinheiro em poder militar?";
		mes "Já sofremos muito com as guerras.";
		mes "Não acredito que as armas sejam a melhor maneira de trazer prosperidade a uma nação.";
		next;
		mes "[Príncipe Croix]";
		mes "Não quero derramar sangue, mas não tenho escolha senão lutar pela chance de paz e pelo bem do meu povo.";
		next;
		switch(select("Sim, quero me juntar a você.:Terminar Conversa")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Príncipe Croix]";
			mes "Muito obrigado. Sinto que posso vencer com a ajuda de aventureiros como você.";
			mes "Agora, por favor desça as escadas e se junte aos seus camaradas que estão se preparando para lutar contra o inimigo.";
			break;
		case 2:
			mes "[Príncipe Croix]";
			mes "Por Maroll!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_crua1",255;
	cutin "bat_crua2",255;
	end;
}

bat_room,161,140,3	script	Pajem de Croix#01	415,{ end; }
bat_room,161,142,3	script	Pajem de Croix#02	415,{ end; }

bat_room,160,159,3	script	General Guillaume	420,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "Precisamos da sua habilidade, ó aventureiro valente, para vencer esta batalha.";
	next;
	switch(select("Qual é o motivo da batalha?:Fale-me sobre o Príncipe Croix")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "Nosso grande rei, Marcel Marollo VII, anda muito doente.";
		mes "Sua Majestade declarou que, entre seus 9 filhos, escolherá a mim ou ao Príncipe Croix como o próximo rei.";
		next;
		mes "[General Guillaume]";
		mes "Dois reis não podem compartilhar uma nação! Somente quem sair vitorioso da batalha determinada por Sua Majestade será coroado.";
		next;
		mes "[General Guillaume]";
		mes "No entanto esta não é apenas uma batalha entre nós dois. Esta batalha vai determinar o futuro deste país.";
		mes "Juro por minha honra que vou provar ser quem pode proteger Maroll das ameaças externas.";
		next;
		switch(select("Sim, quero me juntar a você.:Terminar Conversa")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Bem-vindo ao meu exército, camarada.";
			mes "Seus olhos me dizem que posso confiar em você como soldado.";
			next;
			mes "[General Guillaume]";
			mes "Agora, suba as escadas e prepare-se para a batalha com seus companheiros.";
			mes "Sei que eles vão lhe dar as boas vindas de coração!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Serei eu quem vai capturar a bandeira!";
			break;
		}
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "O 5o. Príncipe Croix atualmente é o Primeiro Ministro de Maroll.";
		mes "Ele acha que todos os assuntos nacionais podem ser discutidos e resolvidos em cima de uma mesa,";
		mes "e acredita na co-existência pacífica com outros países.";
		next;
		mes "[General Guillaume]";
		mes "Ele é ignorante demais para admitir que essa tal paz se constrói com as incontáveis vidas sacrificadas nas guerras, enquanto os cidadãos comuns e nobres vivem sem pensar nos horrores que permitem que eles continuem vivendo.";
		next;
		mes "[General Guillaume]";
		mes "Ele é ingênuo demais para entender a realidade...";
		mes "Não posso deixar Maroll nas mãos de alguém como ele, que vive em um sonho!";
		next;
		mes "[General Guillaume]";
		mes "Suas crenças irreais afundarão este país na pobreza, e deixarão o povo fraco. Se ele se tornar rei, Maroll nunca vai descansar dos massacres causados por outros países.";
		mes "Quero ensinar ele o que faz este pequeno país tão poderoso e próspero: o poder militar!";
		next;
		switch(select("Quero entrar em seu exército!:Terminar Conversa")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Bem-vindo ao meu exército, camarada.";
			mes "Seus olhos me dizem que posso confiar em você como soldado.";
			next;
			mes "[General Guillaume]";
			mes "Agora, suba as escadas e prepare-se para a batalha com seus companheiros.";
			mes "Sei que eles vão lhe dar as boas vindas de coração!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Serei eu quem vai capturar a bandeira!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_kiyom1",255;
	cutin "bat_kiyom2",255;
	end;
}

// Bandeiras
//============================================================
//bat_room,140,160,3	script	Base Guillaume#roomflag1	973,{ end; }
//bat_room,167,160,3	script	Base Guillaume#roomflag2	973,{ end; }
//bat_room,140,139,3	script	Base Croix#roomflag1	974,{ end; }
//bat_room,167,139,3	script	Base Croix#roomflag2	974,{ end; }

// Transportadora da Batalha Campal
//============================================================
bat_room,148,150,5	script	Transportadora#BG	124,{
	mes "[Transportadora]";
	mes "Quer deixar o campo de batalha? Use meu serviço para voltar à cidade.";
	next;
	switch(select("Ir Embora:Não ir Embora")) {
	case 1:
		mes "[Transportadora]";
		switch(bat_return) {
		default:
		case 1:
			setarray .@mapname$[0],"Prontera.","prontera";
			setarray .@xy[0],116,72;
			break;
		case 2:
			setarray .@mapname$[0],"Morroc","moc_ruins";
			setarray .@xy[0],152,48;
			break;
		case 3:
			setarray .@mapname$[0],"Al De Baran.","aldebaran";
			setarray .@xy[0],168,112;
			break;
		case 4:
			setarray .@mapname$[0],"Geffen.","geffen";
			setarray .@xy[0],120,39;
			break;
		case 5:
			setarray .@mapname$[0],"Payon.","payon";
			setarray .@xy[0],161,58;
			break;
		case 6:
			setarray .@mapname$[0],"Lighthalzen.","lighthalzen";
			setarray .@xy[0],159,93;
			break;
		case 7:
			setarray .@mapname$[0],"Rachel.","rachel";
			setarray .@xy[0],115,124;
			break;
		}
		mes "Vou mandar você de volta para "+.@mapname$[0]+".";
		close2;
		warp .@mapname$[1],.@xy[0],.@xy[1];
		break;
	case 2:
		mes "[Transportadora]";
		mes "Estarei aqui se você precisar de meus serviços.";
		close;
	}
	end;
}

-	script	Emissária de Maroll::BatRecruit	728,{
	mes "[Emissária de Maroll]";
	mes "Bom dia, aventureiro.";
	mes "Sou uma amazona de um país distante chamado Reino de Maroll.";
	next;
	mes "[Emissária de Maroll]";
	mes "Os dois príncipes do reino estão batalhando pelo trono, e precisam de soldados experientes como você.";
	mes "Quer colocar seu poder a serviço de um dos príncipes do Reino de Maroll?";
	next;
	switch(select("Participar:Não Participar")) {
	case 1:
		mes "[Emissária de Maroll]";
		mes "Que o deus da guerra o abençoe.";
		close2;
		getmapxy(.@mapname$,.@x,.@y,1);
		if (.@mapname$ == "prontera")
			set bat_return,1;
		else if (.@mapname$ == "moc_ruins")
			set bat_return,2;
		else if (.@mapname$ == "aldebaran")
			set bat_return,3;
		else if (.@mapname$ == "geffen")
			set bat_return,4;
		else if (.@mapname$ == "payon")
			set bat_return,5;
		else if (.@mapname$ == "lighthalzen")
			set bat_return,6;
		else if (.@mapname$ == "rachel")
			set bat_return,7;
		else
			set bat_return,1;
		warp "bat_room",154,150;
		break;
	case 2:
		mes "[Emissária de Maroll]";
		mes "Sempre estarei aqui, esperando por mais soldados. Fique à vontade para voltar, se você se interessar.";
		close;
	}
	end;
}

prontera,123,83,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit1	728
moc_ruins,75,162,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit2	728
aldebaran,146,109,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit3	728
geffen,109,66,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit4	728
payon,189,105,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit5	728
lighthalzen,153,86,5	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit6	728
rachel,149,138,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit7	728

// Warps Adicionais
// Vazio
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Vazio
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// BG Livre
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// BG Livre
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// BG Livre
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// BG Livre
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// BG Livre
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Vazio
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Vazio
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	Funcionária Kafra::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Restaurador
//============================================================
bat_room,138,144,4	script	Restaurador#bg	86,{
	callfunc "repairmain","Restaurador";
	end;
}

/*
// NPC para Gerenciamento / GM
//============================================================
bat_room,1,151,3	script	Switch#batgnd	81,{
	input .@input,0,2000;
	if (.@input == 0) {
		mes "O comando foi cancelado.";
		close;
	}
	else if (.@input == 1854) {
		mes "Posso ajudá-lo?";
		next;
		switch(select("Fechar Campo de Batalha:Abrir Campo de Batalha:Resetar a01:Resetar b01:Resetar a02:Resetar b02")) {
		case 1:
			disablenpc "Oficial do Desfiladeiro#01a";
			disablenpc "Oficial do Desfiladeiro#02a";
			disablenpc "Oficial do Desfiladeiro#01b";
			disablenpc "Oficial do Desfiladeiro#02b";
			disablenpc "Oficial de Flavius#01a";
			disablenpc "Oficial de Flavius#01b";
			disablenpc "Oficial de Flavius#02a";
			disablenpc "Oficial de Flavius#02b";
			break;
		case 2:
			enablenpc "Oficial do Desfiladeiro#01a";
			enablenpc "Oficial do Desfiladeiro#02a";
			enablenpc "Oficial do Desfiladeiro#01b";
			enablenpc "Oficial do Desfiladeiro#02b";
			enablenpc "Oficial de Flavius#01a";
			enablenpc "Oficial de Flavius#01b";
			enablenpc "Oficial de Flavius#02a";
			enablenpc "Oficial de Flavius#02b";
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "Completo";
		close;
	}
}
*/

// Trocador de Emblemas
//============================================================
bat_room,160,150,3	script	Erundek	109,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto! -";
		mes "- No momento, você está carregando -";
		mes "- muitos itens. -";
		mes "- Por favor, alivie a sua carga -";
		mes "- e tente outra vez. -";
		close;
	}
	mes "[Erundek]";
	mes "Você tem emblemas do campo de batalha?";
	mes "Posso trocar Emblemas da Bravura e de Valor por itens de recompensa.";
	next;
	switch(select("Trocar Emblemas:Ver o Catálogo")) {
	case 1:
		mes "[Erundek]";
		mes "Você quer trocar emblemas por quais tipos de itens?";
		mes "Para obter mais informações sobre os itens de recompensa, use nosso ^3131FFCatálogo^000000.";
		next;
		switch(select("Arma:Armadura:Accessório:Consumível")) {
		case 1:
			mes "[Erundek]";
			mes "Você escolheu ^3131FFArma^000000.";
			mes "As seguintes armas estão disponíveis em troca de emblemas.";
			mes "Note que itens que custam ^3131FFEmblemas da Bravura são indicados por (EB)^000000, e os que valem ^3131FFEmblemas de Valor como (EV)^000000.";
			next;
			switch(select("Adaga, Espadas e Lança de Duas Mãos:Cajado, Maça, Machado de Duas Mãos e Shuriken:Arco, Katar, Instrumento Musical e Chicote:Livros e Garras:Revólver, Rifle, Gatling, Espingarda e Lança-Granadas")) {
			case 1:
				mes "[Erundek]";
				mes "As seguintes armas estão disponíveis na categoria ^3131FFAdaga, Espada de Uma Mão, Espada de Duas Mãos e Lança de Duas Mãos.^000000";
				next;
				switch(select("Damascus Mercenária(EB):Damascus Mercenária(EV):Lâmina Gladiadora(EB):Lâmina Gladiadora(EV):Katzbalger de Assalto(EB):Katzbalger de Assalto(EV):Lança de Assalto:Lança de Assalto de Duas Mãos")) {
				case 1:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],13036,1; //BF_Dagger1
					break;
				case 2:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],13037,1; //BF_Dagger2
					break;
				case 3:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],13411,1; //BF_Sword2
					break;
				case 4:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],13410,1; //BF_Sword1
					break;
				case 5:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1183,1; //BF_Two_Handed_Sword1
					break;
				case 6:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1184,1; //BF_Two_Handed_Sword2
					break;
				case 7:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1425,1; //BF_Spear1
					break;
				case 8:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1482,1; //BF_Lance1
					break;
				}
				break;
			case 2:
				mes "[Erundek]";
				mes "As seguintes armas estão disponíveis na categoria ^3131FFCajado, Maça, Machado de Duas Mãos, e Shuriken Huuma.^000000";
				next;
				switch(select("Varinha Mágica do Feiticeiro(EB):Varinha de Batalha do Feiticeiro(EV):Varinha da Recuperação Forte:Varinha da Recuperação Rápida:Maça de Campo de Batalha(EB):Maça de Campo de Batalha(EV):Machado de Batalha Insana(EB):Machado de Batalha Insana(EV):Shuriken Huuma Frontal(EB):Shuriken Huuma Frontal(EV)")) {
				case 1:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1632,1; //BF_Staff1
					break;
				case 2:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1633,1; //BF_Staff2
					break;
				case 3:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1634,1; //BF_Staff3
					break;
				case 4:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1635,1; //BF_Staff4
					break;
				case 5:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1543,1; //BF_Morning_Star2
					break;
				case 6:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1542,1; //BF_Morning_Star1
					break;
				case 7:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1380,1; //BF_Two_Handed_Axe2
					break;
				case 8:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1379,1; //BF_Two_Handed_Axe1
					break;
				case 9:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],13305,1; //BF_Huuma_Shuriken1
					break;
				case 10:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],13306,1; //BF_Huuma_Shuriken2
					break;
				}
				break;
			case 3:
				mes "[Erundek]";
				mes "As seguintes armas estão disponíveis na categoria ^3131FFArco, Katar, Instrumento Musical e Chicote.^000000";
				next;
				switch(select("Besta de Batalha(EB):Besta de Batalha(EV):Katar de Carnificina(EB):Katar de Carnificina(EV):Guitarra de Batalha(EB):Guitarra de Batalha(EV):Serpente de Batalha(EB):Serpente de Batalha(EV)")) {
				case 1:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1739,1; //BF_Bow2
					break;
				case 2:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1738,1; //BF_Bow1
					break;
				case 3:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1279,1; //BF_Katar1
					break;
				case 4:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1280,1; //BF_Katar2
					break;
				case 5:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1924,1; //BF_Instrument2
					break;
				case 6:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1923,1; //BF_Instrument1
					break;
				case 7:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1978,1; //BF_Whip2
					break;
				case 8:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1977,1; //BF_Whip1
					break;
				}
				break;
			case 4:
				mes "[Erundek]";
				mes "As seguintes armas estão disponíveis na categoria ^3131FFLivros e Garras.^000000";
				next;
				switch(select("Livro de Estratégia de Batalha(EB):Livro de Estratégia de Batalha(EV):Punho de Batalha(EB):Punho de Batalha(EV)")) {
				case 1:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1574,1; //BF_Book1
					break;
				case 2:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1575,1; //BF_Book2
					break;
				case 3:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],1824,1; //BF_Knuckle2
					break;
				case 4:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],1823,1; //BF_Knuckle1
					break;
				}
				break;
			case 5:
				mes "[Erundek]";
				mes "As seguintes armas estão disponíveis na categoria ^3131FFRevólver, Rifle, Gatling, Espingarda e Lança-Granadas.^000000";
				next;
				switch(select("Revolver do Soldado:Rifle do Soldado:Metralhadora Gatling do Soldado:Espingarda do Soldado:Lança-Granadas do Soldado")) {
				case 1:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],13108,1; //BF_Pistol1
					break;
				case 2:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],13171,1; //BF_Rifle1
					break;
				case 3:
					setarray .@cost[0],7828,100; //BF_Badge1
					setarray .@item[0],13172,1; //BF_Gatling_Gun1
					break;
				case 4:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],13173,1; //BF_Shotgun1
					break;
				case 5:
					setarray .@cost[0],7829,100; //BF_Badge2
					setarray .@item[0],13174,1; //BF_Launcher1
					break;
				}
				break;
			}
			if (.@cost[0] == 7829)
				set .@type$,"(VB)";
			else 
				set .@type$,"(BB)";
			mes "[Erundek]";
			mes "Você escolheu: ^3131FF"+getitemname(.@item[0])+""+.@type$+"^000000.";
			mes "Você pode trocar ^FF0000"+.@cost[1]+" "+getitemname(.@cost[0])+"^000000 por este item.";
			mes "Quer trocar?";
			next;
			switch(select("Não trocar:Trocar")) {
			case 1:
				mes "[Erundek]";
				mes "Precisa de mais tempo para verificar os itens?";
				break;
			case 2:
				mes "[Erundek]";
				mes "Quer trocar ^FF0000"+.@cost[1]+" "+getitemname(.@cost[0])+"^000000 por uma ^3131FF"+getitemname(.@item[0])+""+.@type$+"^000000?";
				next;
				mes "[Erundek]";
				mes "Lembre-se, as recompensas do Campo de Batalha são ^FF0000Presas ao Personagem^000000. Você tem certeza que deseja abter esse item?";
				next;
				switch(select("Sim:Não")) {
				case 1:
					if (countitem(.@cost[0]) >= .@cost[1]) {
						mes "[Erundek]";
						mes "Obrigado por trocar.";
						delitem .@cost[0],.@cost[1];
						getitem .@item[0],.@item[1];
					}
					else {
						mes "[Erundek]";
						mes "Desculpe, mas você não tem emblemas suficientes.";
					}
					break;
				case 2:
					mes "[Erundek]";
					mes "Precisa de mais tempo para verificar os itens?";
					break;
				}
				break;
			}
			close;
		case 2:
			mes "[Erundek]";
			mes "Você escolheu ^3131FFArmadura.^000000";
			mes "As seguintes armaduras estão disponíveis em troca de emblemas.";
			next;
			switch(select("Capas e Sapatos:Armaduras")) {
			case 1:
				switch(select("Manteau do Capitão:Manteau do Comandante:Manteau do Xerife:Grevas de Batalha:Botas de Combate:Botas de Batalha")) {
				case 1:
					setarray .@cost[0],7828,50,7829,50; //BF_Badge1
					setarray .@item[0],2538,1; //Commander_Manteau
					break;
				case 2:
					setarray .@cost[0],7828,50,7829,50; //BF_Badge1
					setarray .@item[0],2539,1; //Commander_Manteau_
					break;
				case 3:
					setarray .@cost[0],7828,50,7829,50; //BF_Badge1
					setarray .@item[0],2540,1; //Sheriff_Manteau
					break;
				case 4:
					setarray .@cost[0],7828,50,7829,50; //BF_Badge1
					setarray .@item[0],2435,1; //Battle_Greave
					break;
				case 5:
					setarray .@cost[0],7828,50,7829,50; //BF_Badge1
					setarray .@item[0],2436,1; //Combat_Boots
					break;
				case 6:
					setarray .@cost[0],7828,50,7829,50; //BF_Badge1
					setarray .@item[0],2437,1; //Battle_Boots
					break;
				}
				break;
			case 2:
				switch(select("Armadura de Assalto:Armadura de Elite:Manto do Mercenário:Manto de Batalha do Feiticeiro:Manto do Médico:Túnica do Arqueiro de Elite:Túnica do Artilheiro de Elite")) {
				case 1:
					setarray .@cost[0],7828,80,7829,80; //BF_Badge1
					setarray .@item[0],2376,1; //Assaulter_Plate
					break;
				case 2:
					setarray .@cost[0],7828,80,7829,80; //BF_Badge1
					setarray .@item[0],2377,1; //Elite_Engineer_Armor
					break;
				case 3:
					setarray .@cost[0],7828,80,7829,80; //BF_Badge1
					setarray .@item[0],2378,1; //Assassin_Robe
					break;
				case 4:
					setarray .@cost[0],7828,80,7829,80; //BF_Badge1
					setarray .@item[0],2379,1; //Warlock_Battle_Robe
					break;
				case 5:
					setarray .@cost[0],7828,80,7829,80; //BF_Badge1
					setarray .@item[0],2380,1; //Medic_Robe
					break;
				case 6:
					setarray .@cost[0],7828,80,7829,80; //BF_Badge1
					setarray .@item[0],2381,1; //Elite_Archer_Suit
					break;
				case 7:
					setarray .@cost[0],7828,80,7829,80; //BF_Badge1
					setarray .@item[0],2382,1; //Elite_Shooter_Suit
					break;
				}
				break;
			}
			break;
		case 3:
			mes "[Erundek]";
			mes "Você escolheu ^3131FFAcessório.^000000";
			mes "Você pode trocar Emblemas pela Medalha de Honra apropriada à sua classe:";
			next;
			switch(select("Justiceiro:Espadachim, Mestre Taekwon:Gatuno:Noviço:Mago:Arqueiro:Mercador")) {
			case 1:
				setarray .@cost[0],7828,500,7829,500; //BF_Badge1
				setarray .@item[0],2733,1; //Medal_Gunner
				break;
			case 2:
				setarray .@cost[0],7828,500,7829,500; //BF_Badge1
				setarray .@item[0],2720,1; //Medal_Swordman
				break;
			case 3:
				setarray .@cost[0],7828,500,7829,500; //BF_Badge1
				setarray .@item[0],2721,1; //Medal_Thief
				break;
			case 4:
				setarray .@cost[0],7828,500,7829,500; //BF_Badge1
				setarray .@item[0],2722,1; //Medal_Acolyte
				break;
			case 5:
				setarray .@cost[0],7828,500,7829,500; //BF_Badge1
				setarray .@item[0],2723,1; //Medal_Mage
				break;
			case 6:
				setarray .@cost[0],7828,500,7829,500; //BF_Badge1
				setarray .@item[0],2724,1; //Medal_Archer
				break;
			case 7:
				setarray .@cost[0],7828,500,7829,500; //BF_Badge1
				setarray .@item[0],2725,1; //Medal_Merchant
				break;
			}
			break;
		case 4:
			mes "[Erundek]";
			mes "Você escolheu ^3131FFConsumível^000000.";
			mes "Os seguintes itens comsumíveis estão disponíveis em troca de emblemas:";
			next;
			switch(select("Ração Rosa Gostosa:Ração Branca Gostosa:Ração Militar A:Ração Militar B:Ração Militar C")) {
			case 1:
				setarray .@cost[0],7828,10,7829,10; //BF_Badge1
				setarray .@item[0],12269,1; //Tasty_Colonel
				break;
			case 2:
				setarray .@cost[0],7828,10,7829,10; //BF_Badge1
				setarray .@item[0],12270,1; //Tasty_Major
				break;
			case 3:
				setarray .@cost[0],7828,5,7829,5; //BF_Badge1
				setarray .@item[0],12271,1; //Mre_A
				break;
			case 4:
				setarray .@cost[0],7828,10,7829,10; //BF_Badge1
				setarray .@item[0],12272,1; //Mre_B
				break;
			case 5:
				setarray .@cost[0],7828,10,7829,10; //BF_Badge1
				setarray .@item[0],12273,1; //Mre_C
				break;
			}
			break;
		}
		break;
	case 2:
		mes "[Erundek]";
		mes "Temos muitos itens; fique à vontade para olhar antes de comprar.";
		close2;
		ReadBook 11010,1;
		end;
	}

	mes "[Erundek]";
	mes "Você escolheu a ^3131FF"+getitemname(.@item[0])+"^000000.";
	switch(.@item[0]) {
	case 2720: mes "Este item serve somente para Espadachim e Mestre Taekwon."; break;
	case 2721: mes "Este item serve somente para Gatunos."; break;
	case 2722: mes "Este item serve somente para Noviços."; break;
	case 2723: mes "Este item serve somente para Magos."; break;
	case 2724: mes "Este item serve somente para Arqueiros."; break;
	case 2725: mes "Este item serve somente para Mercadores."; break;
	case 2733: mes "Este item serve somente para Justiceiros."; break;
	default: break;
	}
	mes "Você pode trocar ^FF0000"+.@cost[1]+" "+getitemname(.@cost[0])+" ou "+.@cost[3]+" "+getitemname(.@cost[2])+"^000000 por este item.";
	mes "Quer trocar?";
	next;
	switch(select("Não trocar:Trocar")) {
	case 1:
		mes "[Erundek]";
		mes "Precisa de mais tempo para verificar os itens?";
		break;
	case 2:
		mes "[Erundek]";
		mes "Qual Emblema você quer usar para trocar?";
		mes "Você precisar de ^3131FF"+.@cost[1]+" Emblemas^000000 para trocar.";
		next;
		if (.@item[0] < 12269 || .@item[0] > 12273 ) {
			mes "[Erundek]";
			mes "Lembre-se, as recompensas do Campo de Batalha são ^FF0000Presas ao Personagem^000000. Você tem certeza que deseja obter esse item?";
			next;
		}
		switch(select("Emblema de Bravura:Emblema de Valor:Cancelar")) {
		case 1:
			if (countitem(.@cost[0]) >= .@cost[1]) {
				mes "[Erundek]";
				mes "Obrigado por trocar.";
				delitem .@cost[0],.@cost[1];
				getitem .@item[0],.@item[1];
			}
			else {
				mes "[Erundek]";
				mes "Você não tem Emblemas de Bravura suficientes.";
			}
			break;
		case 2:
			if (countitem(.@cost[2]) >= .@cost[3]) {
				mes "[Erundek]";
				mes "Obrigado por trocar.";
				delitem .@cost[2],.@cost[3];
				getitem .@item[0],.@item[1];
			}
			else {
				mes "[Erundek]";
				mes "Você não tem Emblemas de Valor suficientes.";
			}
			break;
		case 3:
			mes "[Erundek]";
			mes "Você cancelou a troca.";
			break;
		}
		break;
	}
	close;
}
