//===== Cronus Script ========================================
//= Sistema de Leilão
//===== Por: =================================================
//= L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.2a
//===== Comentários Adicionais: ==============================
//= 1.0 First version, place holders only. [L0ne_W0lf]
//= 1.1 Moved Morroc Auction NPCs to Morroc Ruins. [L0ne_W0lf]
//= 1.2 Warp fixed. Dialog in Ruins/Lighthalzen changed. [Kisuka]
//= 1.2a Traduzido. [RoM]
//= 1.2b Corrigido um erro nos npc duplicados [TecnoCronus]
//============================================================

// Auction House warpers
//============================================================
moc_ruins,78,173,6	script	Guia do Leilão#moc	98,{
	callfunc "F_AuctionWarper",1;
}

prontera,218,120,4	script	Guia do Leilão#prt	117,{
	callfunc "F_AuctionWarper",2;
}
yuno,129,116,0	script	Guia do Leilão#yuno	90,{
	callfunc "F_AuctionWarper",3;}

lighthalzen,205,169,6	script	Guia do Leilão#lhz	833,{
	callfunc "F_AuctionWarper",4;
}


function	script	F_AuctionWarper	{
	mes "[Guia do Leilão]";
	mes "Olá, você gostaria";
	mes "de entrar na Sala";
	mes "do Leilão?";
	next;
	if (select("Sim:Não") == 1) {
		if (getarg(0) == 1 || getarg(0) == 4) {
			mes "[Guia do Leilão]";
			mes "Ótimo! Então,";
			mes "espero que você se";
			mes "divirta e goste do leilão~";
		}else{
			mes "[Guia do Leilão]";
			mes "Divirta-se no leilão.";
		}
		close2;
		switch(getarg(0)) {
		case 1:
			warp "auction_01",179,53; end;
		case 2:
			warp "auction_01",21,43; end;
		case 3:
			warp "auction_02",151,23; end;
		case 4:
			warp "auction_02",43,24; end;
		}
	}
	mes "[Guia do Leilão]";
	mes "Tudo bem,";
	mes "te vejo depois.";
	mes "Se você mudar de";
	mes "idéia, venha e";
	mes "divirta-se nos leilões~";
	close;
}

// Sign posts
//============================================================
moc_ruins,76,176,6	script	Informações#moc::AuctionSign	837,{
	mes "[Informações]";
	mes "Guia de Teletransporte para o Leilão";
	close;
}

prontera,216,120,4	duplicate(AuctionSign)	Informações#prt	837
yuno,131,116,0	duplicate(AuctionSign)	Informações#yuno	837
lighthalzen,207,169,6	duplicate(AuctionSign)	Informações#lhz	837

// Warps
//============================================================
auction_01,180,49,0	warp	auction_entrance_moc	1,1,moc_ruins,78,171
auction_01,22,37,0	warp	auction_entrance_prt	1,1,prontera,214,120
auction_02,151,17,0	warp	auction_enterance_juno	1,1,yuno,132,119
auction_02,43,17,0	warp	auction_enterance_lhz	1,1,lighthalzen,209,169

// Casas de Leilão
//============================================================
auction_01,182,68,6	script	Corretor do Leilão#moc1::AuctionBroker	98,{
	mes "[Corretor do Leilão]";
	mes "Bem-vindo à Sala do Leilão.";
	mes "Você gostaria de ver o que temos?";
	next;
	if (select("Sim:Não") == 1) {
		mes "[Corretor do Leilão]";
		mes "Muito bem.";
		mes "Por favor dê uma";
		mes "olhada, e veja o";
		mes "que está sendo oferecido~";
		OpenAuction;
		close;
	}
	mes "[Corretor do Leilão]";
	mes "Muito bem, então.";
	mes "Se você mudar de";
	mes "idéia, então volte";
	mes "e dê uma olhada";
	mes "nos leilões~";
	close;
}

auction_01,182,75,0	duplicate(AuctionBroker)	Corretor do Leilão#moc2	99
auction_01,177,75,2	duplicate(AuctionBroker)	Corretor do Leilão#moc3	98
auction_01,177,68,4	duplicate(AuctionBroker)	Corretor do Leilão#moc4	99
auction_01,21,74,4	duplicate(AuctionBroker)	Corretor do Leilão#prt1	117
auction_01,27,78,4	duplicate(AuctionBroker)	Corretor do Leilão#prt2	116
auction_01,16,78,4	duplicate(AuctionBroker)	Corretor do Leilão#prt3	115
auction_02,158,47,6	duplicate(AuctionBroker)	Corretor do Leilão#yuno1	90
auction_02,145,47,2	duplicate(AuctionBroker)	Corretor do Leilão#yuno2	90
auction_02,151,54,0	duplicate(AuctionBroker)	Corretor do Leilão#yuno3	90
auction_02,152,41,4	duplicate(AuctionBroker)	Corretor do Leilão#yuno4	90
auction_02,57,46,2	duplicate(AuctionBroker)	Corretor do Leilão#lhz1	874
auction_02,31,46,6	duplicate(AuctionBroker)	Corretor do Leilão#lhz2	874
auction_02,43,65,4	duplicate(AuctionBroker)	Corretor do Leilão#lhz3	833
